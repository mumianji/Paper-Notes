# (c) Silvaco Inc., 2018
go atlas
TITLE Bipolar frequency response

mesh
x.m l=0 spac=0.1
x.m l=2 spac=0.1

y.m l=0 spac=0.002
y.m l=1 spac=0.10

region num=1 silicon

electrode num=1 name=emitter left length=0.8
electrode num=2 name=base    right length=0.5 y.max=0
electrode num=3 name=collector bottom

doping reg=1 uniform n.type conc=5e15 
doping reg=1 gauss   n.type conc=1e18 peak=1.0 char=0.2
doping reg=1 gauss   p.type conc=1e18 peak=0.05 junct=0.15 
doping reg=1 gauss   n.type conc=5e19 peak=0.0  junct=0.05 x.right=0.8
doping reg=1 gauss   p.type conc=5e19 peak=0.0  char=0.08 x.left=1.5


# set poly emitter and lifetimes
contact name=emitter n.poly surf.rec
material taun0=5e-6 taup0=5e-6

# set bipolar models
models bipolar print  


solve init

save outf=bjtex06.str
tonyplot  bjtex06.str -set bjtex06.set

method  newton  trap 
solve 
solve vcollector=2 local

solve vbase=0.1 
solve vbase=0.2 vstep=0.05 vfinal=0.8 name=base 

log outf=bjtex06.log master
solve prev ac freq=1e6 term=2 fstep=5 mult.f nfstep=10

# plot results
tonyplot  bjtex06.log -set bjtex06_log.set
quit
