# (c) Silvaco Inc., 2018
go atlas
#
# This example demonstrates the use of the high-k mobility model for
# mobility reduction due to remote phonon scattering.  This is based
# in the work in:
#
# Zhu, W. and Ma, T., "Temperature Dependence of CHannel Mobility in 
# HfO2-Gated NMOSFETs", IEEE Elec. Dev. Lett., Vol. 25, No. 2, pp. 89-91,
# 2 Feb. 2004.
#
# The objective of this simulation is to reproduce the data in figure 2
# of that reference.
#
# First define a simple MISFET (2 micron chanel length).  In the first
# run we use a HfO2 gate dielectric.  In the second half we use SiO2
# of equivalent thickness.
#
mesh
x.m l=0.00 s=0.1
x.m l=0.8  s=0.05
x.m l=1.3  s=0.1
x.m l=1.8  s=0.05
x.m l=2.6  s=0.1
#
y.m l=-0.004888 s=0.001
y.m l=0.0       s=0.001
y.m l=0.1       s=0.025
y.m l=1         s=0.2
#
region      num=1 y.min=0 y.max=1 silicon
region      num=2 y.max=0 material=HfO2
electrode   name=gate   x.min=0.6 x.max=2.0 y.max=-0.004888
electrode   name=source x.max=0.5 y.min=0 y.max=0
electrode   name=drain  x.min=2.1 y.min=0 y.max=0
electrode   substrate
doping uniform conc=4e15 p.type   
doping gauss n.type conc=1e20 char=0.075 lat.char=0.05 reg=1 x.r=0.6 
doping gauss n.type conc=1e20 char=0.075 lat.char=0.05 reg=1 x.l=2 
#
# The following line uses the high-k workfunction model.
#
contact name=gate aluminum
#
# Optionally modify workfunction.
#
#contact name=gate highk.al on.HfO2
#
# Here we assign high background mobility to separate out 
# remote phonon effect,
mobility rps.n rps.p mun0=5000
#
# normally this would also be on to include cvt model.
#
#mobility cvt.n cvt.p 
#
method newton carriers=2
#
solve init
solve 
solve vdrain=0.01
#
save outf=highkex01_0.str
#
# Here we probe the normal field and channel charge.  These are
# used in the set file to help calculate effective channel mobility.
#
probe name=Eperp x=1.3 y=0.0001 dir=90 field
probe name=charge x.min=1.1 x.max=1.5 n.conc integrate y.min=0 y.max=0.2
#
log     outf=highkex01_0.log
solve   vstep=0.05 name=gate vfinal=1.0 
#
save outf=highkex01_1.str
#
go atlas 
#
# The second half of the deck repeats the experiment for SiO2 
# gate dielectric.
#
mesh
x.m l=0.0  s=0.1
x.m l=0.8  s=0.05
x.m l=1.3  s=0.1
x.m l=1.8  s=0.05
x.m l=2.6  s=0.1
y.m l=-0.002    s=0.001
y.m l=0.0       s=0.001
y.m l=0.1       s=0.025
y.m l=1         s=0.2
#
region      num=1 y.min=0 y.max=1 silicon
region      num=2 y.max=0 oxide
electrode   name=gate   x.min=0.6 x.max=2.0 y.max=-0.002
electrode   name=source x.max=0.5 y.min=0 y.max=0
electrode   name=drain  x.min=2.1 y.min=0 y.max=0
electrode   substrate
doping uniform conc=4e15 p.type   
doping gauss n.type conc=1e20 char=0.075 lat.char=0.05 reg=1 x.r=0.6 
doping gauss n.type conc=1e20 char=0.075 lat.char=0.05 reg=1 x.l=2 
#
contact name=gate aluminum
#
# Optionally modify workfunction.
#
#contact name=gate highk.al on.SiO2
#
# Here we assign high background mobility to separate out 
# remote phonon effect.
#
mobility rps.n rps.p mun0=5000 mun.rps=323.8 alphan.rps=-0.505 betan.rps=-1.98
#
# Normally this would also be on to include cvt model.
#
#mobility cvt.n cvt.p 
#
method newton carriers=2
#
solve init
solve 
solve vdrain=0.01
#
save outf=highkex01_2.str
probe name=Eperp x=1.3 y=0.0001 dir=90 field
probe name=charge x.min=1.1 x.max=1.5 n.conc integrate y.min=0 y.max=0.2
#
log     outf=highkex01_1.log
solve   vstep=0.05 name=gate vfinal=1.0 
#
save outf=highkex01_3.str
#
# This plot compares effective mobility vs. perpendicular field for
# HfO2 and SiO2 gate dielectrics against experiment.
#
tonyplot -overlay highkex01_0.log highkex01_0.dat highkex01_1.log highkex01_1.dat -set highkex01.set
#  
quit
