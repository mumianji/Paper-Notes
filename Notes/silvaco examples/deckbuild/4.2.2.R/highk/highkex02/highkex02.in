# (c) Silvaco Inc., 2018
go atlas
#
# This example demonstrates the use of the high-k mobility model for
# mobility reduction due to remote Coulomb scattering.  This is based
# in the work in:
#
# Tanimoto, H. et.al., "Modeling of Electron Mobility Degradiation for
# HfSiON MISFETs", Proc. of SISPAD (2006), pp. 47-50.
#
# The objective of this simulation is to reproduce the data in figure 2
# of the reference:.
#
# Zhu, W. and Ma, T., "Temperature Dependence of CHannel Mobility in 
# HfO2-Gated NMOSFETs", IEEE Elec. Dev. Lett., Vol. 25, No. 2, pp. 89-91,
# 2 Feb. 2004.
#
# First define a simple MISFET (2 micron chanel length)
# (Commented out lines are for SiO2 of equivalent 2 nm EIT)
#
mesh
x.m l=0.00 s=0.1
x.m l=0.8  s=0.05
x.m l=1.3  s=0.1
x.m l=1.8  s=0.05
x.m l=2.6  s=0.1
#	
y.m l=-0.004888 s=0.001
#y.m l=-0.002   s=0.001
y.m l=0.0       s=0.001
y.m l=0.1       s=0.025
y.m l=1         s=0.2
#
region      num=1 y.min=0 y.max=1 silicon
region      num=2 y.max=0 material=HfO2
#region      num=2 y.max=0 oxide 
electrode   name=gate   x.min=0.6 x.max=2.0 y.max=-0.004888
#electrode   name=gate   x.min=0.6 x.max=2.0 y.max=-0.002
electrode   name=source x.max=0.5 y.min=0 y.max=0
electrode   name=drain  x.min=2.1 y.min=0 y.max=0
electrode   substrate
doping uniform conc=3e16 p.type   
doping gauss n.type conc=1e20 char=0.075 lat.char=0.05 reg=1 x.r=0.6 
doping gauss n.type conc=1e20 char=0.075 lat.char=0.05 reg=1 x.l=2 
#
# the following line uses the high-k workfunction model
#
contact name=gate highk.al on.HfO2
#contact name=gate aluminum
#
# Here enable remote coulomb scattering model.
# Assign low background mobility to separate out
# rcs model.
#
mobility rcs.n rcs.p mun0=0.001 mup0=0.001
#
# Normally we should also enable cvt model.
#
#mobility cvt.n cvt.p 
#
method newton carriers=2
#
solve init
solve 
solve vdrain=0.01
#
save outf=highkex02_0.str
#
# Here we probe the normal field and channel charge.  These are
# used in the set file to help calculate effective channel mobility.
#
probe name=Eperp x=1.3 y=0.0001 dir=90 field
probe name=charge x=1.3 y=0.00001 n.conc
#
log     outf=highkex02.log
solve   vstep=0.01 name=gate vfinal=0.5 
#
save outf=highkex02_1.str
#
# This plot compares simulated effective mobility versus perpendicular
# field versus experiment.
#
tonyplot -overlay highkex02.log highkex02.dat -set highkex02.set
#
quit
