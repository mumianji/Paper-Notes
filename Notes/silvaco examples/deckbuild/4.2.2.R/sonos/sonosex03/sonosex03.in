# (c) Silvaco Inc., 2018
# Example, of dynamic sonos code,  charging by tunnelling
# followed by erasing by tunnelling.
go atlas

set vgate=18

mesh auto width=0.25


x.m  loc=0.0  spac=0.025
x.m  loc=0.1  spac=0.005
x.m  loc=0.2  spac=0.005
x.m  loc=0.3  spac=0.025


y.m  loc=-0.012  spac=0.0005
y.m  loc=-0.007  spac=0.00025
y.m  loc=-0.002  spac=0.00025
y.m  loc=0.0     spac=0.001
y.m  loc=0.1     spac=0.01
y.m  loc=1.0     spac=0.2

region num=1 silicon y.min=0.0
region num=2 oxide y.max=0.0
region num=3 nitride y.min=-0.007 y.max=-0.002 x.min=0.1 x.max=0.2

electrode name=gate top x.min=0.1 x.max=0.2
electrode name=source y.min=0.0 y.max=0.0 x.max=0.05
electrode name=drain  y.min=0.0 y.max=0.0 x.min=0.25
electrode name=substrate bottom

doping uniform conc=1e18 p.type region=1
doping gauss   conc=1e20 n.type junc=0.05 x.right=0.1 ratio.lat=0.1 region=1
doping gauss   conc=1e20 n.type junc=0.05 x.left=0.2  ratio.lat=0.1 region=1 

interface n.i 

# Reducing the effective mass in the barrier increases the tunnelling rate
material material=oxide semiconductor nc300=1e19 nv300=1e19 taun0=1.0 taup0=1.0 mc=0.4 mv=0.4

# Change the mobilities to get different transport inside the film
material material=nitride semiconductor nc300=1e19 nv300=1e19 mun=1.0e-1 mup=1.0e-1 mc=0.4 mv=0.4 taun0=1.0 taup0=1.0

contact name=gate n.poly

nitridecharge  nt.p=0.0 nt.n=1.0e20 tau.n=1.0e-4 tau.p=1.0e-4 elec.depth=1.5 hole.depth=1.5 sigmat.n=1.0e-10 sigmat.p=1.0e-10 sigman.p=1.0e-15 sigmap.n=1.0e-15 pf.barrier=1.5

# probe the total charged stored in the Nitride layer
probe name=nettcharge sonos.charge integrate y.min=-0.0075 y.max=-0.0015 x.min=0.1 x.max=0.2

models srh

method carriers=2 

solve init sonos

solve prev 

output sonos.rates val.band con.band band.temp



# Charge up the Nitride layer by tunnelling
log outf=sonosex03_charge.log j.tun sonos.curr
solve vgate=$vgate ramptime=1e-9 tstep=1e-12 tfinal=1e-9  proj 

method dt.min=1.0e-9
solve tstop=1.0e-8 proj
save outf=sonosex03_charging_m8.str
solve tstop=1.0e-7 proj
method dt.min=1.0e-8
solve tstop=1e-6 proj
save outf=sonosex03_charging_m6.str
method dt.min=1.0e-7
solve tstop=1e-5 proj
method dt.min=1.0e-6
solve tstop=1e-4 proj
save outf=sonosex03_charging_m4.str
method dt.min=1.0e-5
solve tstop=1e-3 proj
method dt.min=1.0e-4
solve tstop=1e-2 proj
save outf=sonosex03_charging_m2.str
method dt.min=1.0e-3
solve tstop=1.0e-1 proj
solve tstop=1.0 proj
save outf=sonosex03_charging_m0.str master
log off

# Solve for values of 0 V bias on each contact
# the sonos parameter on solve init preserves the
# large fixed charge in the nitride layer and prevents
# numerical problems caused by steady state compensation.

solve init sonos

solve prev

method dt.min=1.0e-14 itlimit=10

# discharge the device by tunnelling by applying negative
# bias to the gate.

log outf=sonosex03_erase_1_5.log j.tun sonos.curr
solve vgate=-10.0 ramptime=1e-9 tstep=1e-12 tfinal=1.0e-9 

output sonos.rates val.band con.band band.temp

solve tstop=1.0e-8 proj
save outf=sonosex03_erasing_m8.str

solve tstop=1.0e-7 proj

method dt.min=1.0e-8
solve tstop=1.0e-6 proj
save outf=sonosex03_erasing_m6.str

method dt.min=1.0e-7
solve tstop=1.0e-5 proj

method dt.min=1.0e-6
solve tstop=1.0e-4 proj
save outf=sonosex03_erasing_m4.str

method dt.min=1.0e-5
solve tstop=1.0e-3 proj

method dt.min=1.0e-4
solve tstop=1e-2 proj
save outf=sonosex03_erasing_m2.str

method dt.min=1.0e-3
solve tstop=1.0e-1 proj

method dt.min=1.0e-2
solve tstop=1.0 proj
save outf=sonosex03_erasing_m0.str
log off

# End first erasing run
# Reissue nitridecharge statement to set trap depths to 2.0 V. This 
# will reduce rate of erasing

nitridecharge nt.p=0.0 nt.n=1.0e20 tau.n=1.0e-4 tau.p=1.0e-4 elec.depth=2.0 hole.depth=2.0 sigmat.n=1.0e-10 sigmat.p=1.0e-10 sigman.p=1.0e-15 sigmap.n=1.0e-15 pf.barrier=1.5

# load in the fully charged structure
load infile=sonosex03_charging_m0.str master

contact name=gate n.poly

solve init sonos

solve prev

method dt.min=1.0e-14 itlimit=10

# discharge the device by tunnelling by applying gate bias of -10 V
log outf=sonosex03_erase_2_0.log j.tun sonos.curr
solve vgate=-10.0 ramptime=1e-9 tstep=1e-12 tfinal=1.0e-9 

solve tstop=1.0e-8 proj

solve tstop=1.0e-7 proj

method dt.min=1.0e-8
solve tstop=1.0e-6 proj

method dt.min=1.0e-7
solve tstop=1.0e-5 proj

method dt.min=1.0e-6
solve tstop=1.0e-4 proj

method dt.min=1.0e-5
solve tstop=1.0e-3 proj

method dt.min=1.0e-4
solve tstop=1e-2 proj

method dt.min=1.0e-3
solve tstop=1.0e-1 proj

method dt.min=1.0e-2
solve tstop=1.0 proj
log off

# End second erasing run


# We can use the tonyplot cutline tool to view the time dependence of
# the profiles of trapped charge etc in the dielectric gate stack. 

tonyplot -overlay sonosex03_charging_m8.str sonosex03_charging_m6.str sonosex03_charging_m4.str sonosex03_charging_m2.str sonosex03_charging_m0.str -set sonosex03_0.set

tonyplot -overlay sonosex03_erasing_m8.str sonosex03_erasing_m6.str sonosex03_erasing_m4.str sonosex03_erasing_m2.str sonosex03_erasing_m0.str -set sonosex03_1.set

tonyplot -overlay sonosex03_charge.log  sonosex03_erase_1_5.log sonosex03_erase_2_0.log -set sonosex03_2.set


quit

