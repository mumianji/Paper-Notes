# (c) Silvaco Inc., 2018
# This deck loops 4 times with different values of capture cross-section

loop steps=4

assign name=xs    n.value=1.0e-14 ratio=0.1

assign name=fname c.value="1_0E-14" delta=1


go atlas 

mesh width=1.0 

x.m  loc=0.0  spac=0.02
x.m  loc=0.1  spac=0.005
x.m  loc=0.2  spac=0.005
x.m  loc=0.3  spac=0.02
 
y.m  loc=-0.012  spac=0.001
y.m  loc=-0.0075 spac=0.0002
y.m  loc=-0.002 spac=0.0002
y.m  loc=0.0    spac=0.00025
y.m  loc=0.01   spac=0.0025
y.m  loc=0.1    spac=0.01
y.m  loc=1.0    spac=0.2

# set up a typical MOSFET structure with a nitride layer in gate stack

region num=1 silicon y.min=0.0
region num=2 oxide y.max=0.0
region num=3 nitride y.min=-0.0075 y.max=-0.002 x.min=0.1 x.max=0.2

electrode name=gate x.min=0.1 x.max=0.2 y.min=-0.012 y.max=-0.012
electrode name=source y.min=0.0 y.max=0.0 x.max=0.05
electrode name=drain  y.min=0.0 y.max=0.0 x.min=0.25

electrode name=substrate bottom

doping uniform conc=1e18 p.type region=1

doping gauss   conc=1e20 n.type junc=0.15 x.right=0.05 ratio.lat=0.2 region=1
doping gauss   conc=1e20 n.type junc=0.15 x.left=0.25  ratio.lat=0.2 region=1 

# This enables the sophisticated SONOS model, DYNASONOS (this flag is true by default)
# and sets the interface with the Nitride region to be thermionic.
interface n.i dynasonos

# Change all the materials in the insulating gate stack to be wide band
# gap semiconductors and change their properties.
 
# Reducing the effective mass in the barrier increases the tunnelling rate
material material=oxide semiconductor taun0=1.0 taup0=1.0 mc=0.35 mv=1.0 nc300=1e19 nv300=1e19

# Change the mobilities to get different transport inside the film

material material=nitride semiconductor nc300=1e19 nv300=1e19 mun=1.0e-1 mup=1.0e-2 mc=1.0 mv=1.0 taun0=1.0 taup0=1.0

contact name=gate n.poly

# The trap densities in the nitride are set by nt.n (electrons)
# and nt.p (holes).  If the trap density for a carrier type is
# set to zero then the trap equation is not solved for that carrier
# type.  
# The capture cross-sections (sigmat.n and sigmat.p) can be used to
# control the position of the charge centroid in the Nitride. The
# opposite carrier cross-sections (sigman.p and sigmap.n) are less
# important. The emission rates obtained from tau.n and tau.p will 
# affect the overall trapped charge density, but are fixed for this example.

nitridecharge  nt.p=0.0e20  nt.n=5.0e19  tau.n=1.0e-4 tau.p=1.0e-4 elec.depth=1.5 sigmat.n=${xs} sigman.p=0.0 sigmap.n=0.0

# the sonos.charge field on the probe statement retruns the net trapped charge 
probe name=nettcharge sonos.charge integrate y.min=-0.0075 y.max=-0.002 x.min=0.1 x.max=0.2

method dt.max=1.0e-5

models srh

solve init

solve prev

# SONOS.CURR on log statement calculates the currents into
# and out of the nitride region. J.TUN outputs all electrode
# tunnelling currents.
log outf=sonosex01_${fname}.log j.tun sonos.curr
# Charge up the Nitride region
solve vgate=16 ramptime=1e-9 tstep=1e-12 tfinal=1.0e-3
log off

output sonos.rates con.band val.band

save outf=sonosex01_${fname}.str master

l.end

tonyplot -overlay sonosex01_1_0E-14.log sonosex01_1_0E-15.log sonosex01_1_0E-16.log  sonosex01_1_0E-17.log -set sonosex01_0.set

tonyplot -overlay sonosex01_1_0E-14.log sonosex01_1_0E-15.log sonosex01_1_0E-16.log  sonosex01_1_0E-17.log -set sonosex01_1.set

tonyplot -overlay sonosex01_1_0E-14.str sonosex01_1_0E-15.str sonosex01_1_0E-16.str  sonosex01_1_0E-17.str -set sonosex01_2.set
