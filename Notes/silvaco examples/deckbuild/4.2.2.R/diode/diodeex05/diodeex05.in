# (c) Silvaco Inc., 2018
go athena
# 
line x loc=0.00 spac=0.2
line x loc=1 spac=0.1
line x loc=1.1 spac=0.02
line x loc=2 spac=0.25
#
line y loc=0.00 spac=0.02
line y loc=0.2 spac=0.1
line y loc=0.4 spac=0.02
line y loc=2 spac=0.5
# 
init silicon c.phos=5.0e18 orientation=100 
# 
deposit oxide thick=0.50 divisions=5
# 
etch oxide left p1.x=1
# 
implant boron dose=1.0e15 energy=50 pearson tilt=7 rotation=0 amorph
# 
method fermi compress
diffus time=30 temp=1000 nitro press=1.00
#
extract name="xj" xj material="Silicon" mat.occno=1 x.val=0 junc.occno=1
#
extract name="rho" sheet.res material="Silicon" mat.occno=1 x.val=0 region.occno=1

etch oxide all
deposit alum thickness=0.2 div=3
etch alum right p1.x=1.0

electrode name=anode x=0.0
electrode name=cathode backside

structure outf=diodeex05_0.str
tonyplot  diodeex05_0.str -set diodeex05_0.set 

go atlas

models bipolar bbt.std print
impact selb 
 
method newton trap maxtrap=10 climit=1e-4
solve init

log outf=diodeex05.log
solve vanode=-0.25 vstep=-0.25 vfinal=-10 name=anode

tonyplot diodeex05.log -set diodeex05_log.set

extract init infile="diodeex05.log"
extract name="bv" min(v."anode")
extract name="leakage" x.val from curve(v."anode",abs(i."anode")) where y.val=1e-10

 
quit
