# (c) Silvaco Inc., 2018
go athena

#TITLE: SOI Process and Device Simulation Example

# Substrate mesh definition
line y loc=0    spac=0.02   tag=top
line y loc=0.13  spac=0.01
line y loc=0.2  spac=0.02   tag=oxtop
line y loc=0.7  spac=0.5
line y loc=1.2  spac=0.1    tag=oxbot
line y loc=2    spac=0.5    tag=bot

line x loc=-1   spac=0.5    tag=left
line x loc=0    spac=0.1
line x loc=1.55  spac=0.01
line x loc=2    spac=0.25    tag=right
#
region silicon xlo=left xhi=right ylo=top yhi=oxtop
region oxide xlo=left xhi=right ylo=oxtop yhi=oxbot
region silicon xlo=left xhi=right ylo=oxbot yhi=bot
#
init orient=100 c.phos=1e14 space.mult=1.5
#
# pad oxide and nitride mask
deposit oxide thick=0.02 div=1
deposit nitride thick=0.1 div=1
#
# from now on the situation is 2-D
#
etch    nitride left p1.x=0
etch    oxide   left p1.x=0
#
# field oxidation
# 
method compress fermi
diffus time=90 temp=1000 weto2 press=1.00 hcl.pc=0
#
etch nitride all
#
etch oxide start x=1 y=-1
etch continue x=3 y=-1
etch continue x=3 y=0.1
etch done x=1 y=0.1
#
# pwell formation including masking off of the nwell
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
# P-well Implant 
implant boron dose=8e12 energy=100 pears 

# N-well implant not shown -
#
# welldrive starts here
diffus time=220 temp=1200 nitro press=1
#
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide thickness=0.03
#
# gate oxide grown here:-
diffus time=11 temp=925 dryo2 press=1.00 hcl=3

# extract a design parameter.....

extract name="gateox" thickness oxide mat.occno=1 x.val=2.0
#
# vt adjust implant 
implant boron dose=9.5e11 energy=10 pearson 
#
# polysilicon gate formation
depo poly thick=0.2 divi=2 c.phosphor=2e20
#
etch poly left p1.x=1.5
#
# oxidise polysilicon
method fermi compress
diffuse time=3 temp=900 weto2 press=1.0
#
# N- LDD implant
implant phosphor dose=3.0e13 energy=20 pearson 
#
# create sidewalls
depo oxide thick=0.120 divisions=8
#
etch oxide dry thick=0.120
#
# S/D implant
implant arsenic dose=5.0e15 energy=50 pearson 
#
method fermi compress
diffuse time=2 temp=900 nitro press=1.0
#
etch oxide thickness=0.02
#
# metal deposition and patterning for S/G/D
deposit alumin thick=0.03 divi=2
#
struct mirror right
#
etch alumin right p1.x=4.5
etch alumin left p1.x=-0.5
#
etch alumin start x=1.25 y=-1
etch continue x=2.75 y=-1
etch continue x=2.75 y=0.1
etch done x=1.25 y=0.1
#
# electrode definition
electrode name=gate x=2.0 y=-0.1
electrode name=source x=0.0
electrode name=drain x=4.0
electrode name=substrate backside
#
# Save Final Structure
structure outfile=soiex08_0.str
#
tonyplot soiex08_0.str -set soiex08_0.set
#
go atlas
#
#
# define the Gate workfunction....
contact name=gate n.poly

# Define the Gate Qss
interface qf=3e10

# Use the cvt mobility model for MOS....
models cvt srh   hcte.el lat.temp

impact selb

thermcontact num=1 elec.num=4 ext.temp=300

# initial solution to get it started.....
method newton trap autonr maxtrap=10

solve init
#
output     e.field j.electron j.hole j.conduc j.total ex.field ey.field \
        flowlines e.mobility h.mobility qss e.temp h.temp val.band con.band \
        qfn qfp j.disp photogen impact

# apply a small initial drain bias
solve vfinal=0.25 vstep=0.05 name=drain

# solve for Vg=1,3,5V
solve vfinal=1 vstep=0.25 name=gate
save outf=soiex08_1.str

solve vfinal=3 vstep=0.25 name=gate
save outf=soiex08_3.str

solve vfinal=5 vstep=0.25 name=gate
save outf=soiex08_5.str

# obtain Id-Vd curve for Vg=1V
load infile=soiex08_1.str master
solve prev
log outf=soiex08_1.log
solve vfinal=5 vstep=0.25 name=drain
save outf=soiex08_1a.str

log outf=tmp

# obtain Id-Vd curve for Vg=3V
load infile=soiex08_3.str master
solve prev
log outf=soiex08_3.log
solve vfinal=5 vstep=0.25 name=drain
save outf=soiex08_3a.str

log outf=tmp

# obtain Id-Vd curve for Vg=5V
load infile=soiex08_5.str master
solve prev 
log outf=soiex08_5.log
solve vfinal=5 vstep=0.25 name=drain
save outf=soiex08_5a.str

tonyplot -overlay soiex08_1.log soiex08_3.log soiex08_5.log -set soiex08_1.set
tonyplot -overlay soiex08_1.log soiex08_3.log soiex08_5.log -set soiex08_2.set
tonyplot soiex08_5a.str -set soiex08_5a.set

quit
