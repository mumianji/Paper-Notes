
go athena 

#TITLE: MC Plasma Etch Example. 
#Demonstration of Polymer Redeposition Effect

# Substrate mesh definition
line y loc=0    spac=0.04
line y loc=2  spac=0.04

 

line x loc=-0.5   spac=0.1
line x loc=-0.2 spac=0.025
line x loc=0 spac=0.05

init orient=100  

# Make 0.4 um opening in oxide mask 
depo oxide thick=0.5 div=10
etch oxide right p1.x=-0.2
structure mirror right

# Save initial structure 
struct outf=anelex33_0.str

#Specify parameters for MCETCH machine for all 3 participating materials

rate.etch machine=MCETCH silicon mc.plasma ion.types=2 mc.parts1=10000 mc.norm.t1=10.0 mc.lat.t1 = 1.0 mc.ion.cu1=10 mc.etch1=1e-05 mc.alb1=0.2 mc.plm.alb=0.9 mc.parts2=10000 mc.norm.t2=6.0 mc.lat.t2 = 1.0 mc.ion.cu2=5 mc.etch2=1e-05 mc.alb2=0.2 mc.polympt=3000 mc.rflctdif=0.5

# Plasma parameters ion.types=2 mc.parts1,  mc.norm.t1,  mc.lat.t1, 
# mc.ion.cu1, mc.parts2,  mc.norm.t2,  mc.lat.t2,  mc.ion.cu2 should
# be specified only in one rate.etch statement

# Set etch rate for oxide (mask) to zero, while polymer reflection (mc.plm.alb)
# is set to small calue of 0.1

rate.etch machine=MCETCH oxide   mc.plasma   mc.etch1=0.0 mc.alb1=0.9 \ 
mc.etch2=0.0 mc.alb2=0.9 mc.plm.alb=0.1 mc.rflctdif=0.5

# Set etch rate for polymer equal to that of silicon, while reflection
# of polymer from already redeposited polymer (mc.plm.alb) much lower 
# then for silicon but higher then for oxide

rate.etch machine=MCETCH material=polymer   mc.plasma   mc.etch1=1e-05 \
 mc.alb1=0.2  mc.etch2=1e-05 mc.alb2=0.2 mc.plm.alb=0.5 mc.rflctdif=0.5

# First run the machine with redeposition (mc.redepo=t)
# Time steps are selected twice as high as the default ( mc.dt.fact=2)

etch machine=MCETCH time=3 minutes mc.sm=0.001 mc.redepo=t mc.dt.fact=2 

structure outf=anelex33_1.str remove.gas

# The same but without polymer redeposition

init inf=anelex33_0.str
etch machine=MCETCH time=3 minutes mc.sm=0.001 mc.redepo=f mc.dt.fact=2

structure outf=anelex33_2.str remove.gas

tonyplot anelex33_1.str anelex33_2.str

