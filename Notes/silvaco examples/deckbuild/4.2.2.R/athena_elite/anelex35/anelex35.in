
go athena 

#TITLE: MC Plasma Etch Example. 
#Effect of Relative Plasma Temperature

# Substrate mesh definition
line y loc=0    spac=0.04
line y loc=2  spac=0.04

 

line x loc=-0.5   spac=0.1
line x loc=-0.2 spac=0.025
line x loc=0 spac=0.05

init orient=100  

# Make 0.4 um opening in oxide mask 
depo oxide thick=0.5 div=10
etch oxide right p1.x=-0.2
structure mirror right

# Save initial structure 
struct outf=anelex35_0.str

#Specify parameters for MCETCH machine for all 3 participating materials

rate.etch machine=MCETCH silicon mc.plasma ion.types=1 mc.parts1=20000 mc.norm.t1=14.0 mc.lat.t1 = 2.0 mc.ion.cu1=15 mc.etch1=1e-05 mc.alb1=0.2 mc.plm.alb=0.5  mc.polympt=5000 mc.rflctdif=0.5


rate.etch machine=MCETCH oxide   mc.plasma   mc.etch1=0.0 mc.alb1=0.9 \ 
 mc.plm.alb=0.5 mc.rflctdif=0.5


rate.etch machine=MCETCH material=polymer   mc.plasma   mc.etch1=1e-05 \
 mc.alb1=0.2   mc.plm.alb=0.3 mc.rflctdif=0.5

etch machine=MCETCH time=3 minutes mc.sm=0.001 mc.redepo=t mc.dt.fact=2 

structure outf=anelex35_1.str remove.gas


# The same but with higher normal plasma temperature 

init inf=anelex35_0.str
rate.etch machine=MCETCH silicon mc.plasma ion.types=1 mc.parts1=20000 mc.norm.t1=15.0 mc.lat.t1 = 1.0 mc.ion.cu1=15

etch machine=MCETCH time=3 minutes mc.sm=0.001 mc.redepo=t mc.dt.fact=2
structure outfile=anelex35_2.str remove.gas

tonyplot  anelex35_1.str anelex35_2.str
