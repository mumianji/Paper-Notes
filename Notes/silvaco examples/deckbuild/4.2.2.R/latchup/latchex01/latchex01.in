# (c) Silvaco Inc., 2018
go athena cutfile=latchex01.sec
 
# grid and mask information provided by latchex01.sec

init silicon c.boron=1.0e15 orientation=100 

deposit oxide thickness=0.05 div=1
mask name="PWE"
implant amorphous boron dose=3e12 energy=100
strip

mask name="PWE" reverse
implant amorphous phosphorus dose=4e12 energy=180 
strip

# relax grid
relax y.min=1
relax y.min=2 

# well drive 
diffus time=300 temp=1150 

mask name="N+"
implant amorphous arsenic dose=1e15 energy=100 

strip

mask name="P+"
implant amorphous boron dose=1e15 energy=30
strip

# final activation
diffuse time=30 temp=900

etch oxide all
deposit alum thickness=0.05 div=2
mask name="MET"
etch alum dry thickness=0.06
strip

autoelectrode

structure outf=latchex01_0.str
tonyplot  latchex01_0.str -set latchex01_0.set





go atlas
#  Electrodes #1 - pwell; #2 - vss;  #3 - vdd; #4 - nwell;
#mesh     inf=latchex01_0.str

material taup0=1e-6 taun0=1e-6
models   bipolar print 
impact   selb 
output   flowlines 

method   newton trap 

solve  vnwell=0.25 vstep=0.25 vfinal=5 name=nwell
solve  vvdd=0.25 vstep=0.25 vfinal=5 name=vdd 
save outf=latchex01_1.str 
 

log outf=latchex01.log


solve  v2=-1.0 ramptime=0.5e-9 tstep=5.0e-12 tstop=3.0e-9 
save outf=latchex01_2.str 

solve  v2=0 ramptime=0.5e-9 tstep=5.0e-11 tstop=1.e-6  
save outf=latchex01_3.str 

tonyplot latchex01.log -set latchex01_log.set
tonyplot latchex01_1.str latchex01_2.str latchex01_3.str -set latchex01_1.set

quit


