# (c) Silvaco Inc., 2018

go athena

line x loc=0 spac=0.1 
line x loc=0.2 spac=0.01
line x loc=0.5 spac=0.01 
#
line y loc=0.00 spac=0.01 
line y loc=0.2 spac=0.01
line y loc=0.5 spac=0.05
line y loc=0.8 spac=0.15 
#
init orientation=100 c.phos=1e14 space.mul=3

#pwell formation including masking off of the nwell
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#P-well Implant
# 
implant boron dose=8e12 energy=100 pears 
#
diffus temp=950 time=100 weto2 hcl=3
#
#N-well implant not shown -
#
# welldrive starts here
diffus time=50 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
#
diffus time=220 temp=1200 nitro press=1
#
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
diffus time=11 temp=925 dryo2 press=1.00 hcl=3
#
#

#
#vt adjust implant 
implant boron dose=9.5e11 energy=10 pearson 
#
depo poly thick=0.2 divi=10 
#
#from now on the situation is 2-D
#
etch poly left p1.x=0.35
#
method fermi compress
diffuse time=3 temp=900 weto2 press=1.0
#
implant phosphor dose=3.0e13 energy=20 pearson 
#
depo oxide thick=0.120 divisions=8
#
etch oxide dry thick=0.120
#
implant arsenic dose=5.0e15 energy=50 pearson 
#
method fermi compress
diffuse time=1 temp=900 nitro press=1.0
#

#
etch oxide left p1.x=0.2
deposit alumin thick=0.03 divi=2
etch alumin right p1.x=0.18

# Extract a design parameter.....
extract name="gateox" thickness oxide mat.occno=1 x.val=0.49
# Extract another design parameters...
# extract final S/D Xj...
extract name="nxj" xj silicon mat.occno=1 x.val=0.1 junc.occno=1
# extract the long chan Vt...
extract name="n1dvt" 1dvt ntype vb=0.0 qss=1e10 x.val=0.49
# extract a curve of conductance versus bias....
extract start material="Polysilicon" mat.occno=1 bias=0.0 bias.step=0.2 bias.stop=2 x.val=0.45
extract done name="sheet cond v bias" curve(bias,1dn.conduct material="Silicon" mat.occno=1  region.occno=1) outfile="extract.dat"
# extract the N++ regions sheet resistance...
extract name="n++ sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.05 region.occno=1
# extract the sheet rho under the spacer, of the LDD region...
extract name="ldd sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.3 region.occno=1
# extract the surface conc under the channel....
extract name="chan surf conc" surf.conc impurity="Net Doping" material="Silicon" mat.occno=1 x.val=0.45

structure mirror right

electrode name=gate x=0.5 y=0.1
electrode name=source x=0.1
electrode name=drain x=0.9
electrode name=substrate backside


go devedit 

# Set Meshing Parameters
#
base.mesh height=0.1 width=0.1
#
bound.cond !apply max.slope=28 max.ratio=300 rnd.unit=0.001 line.straightening=1 align.points when=automatic
#
imp.refine imp="NetDoping" sensitivity=1
imp.refine min.spacing=0.02
#
constr.mesh max.angle=90 max.ratio=300 max.height=1 \
	max.width=1 min.height=0.0001 min.width=0.0001
#
# Perform mesh operations
#
Mesh Mode=MeshBuild
structure outf=mos1ex06_0x.str

refine mode=y x1=0.34 y1=0.22 x2=0.65 y2=0.24
refine mode=y x1=0.35 y1=0.22 x2=0.67 y2=0.23
refine mode=both x1=0.65 y1=0.26 x2=0.82 y2=0.34
refine mode=y x1=0 y1=0.40 x2=1.0 y2=0.57
refine mode=y x1=0 y1=0.40 x2=1.0 y2=0.53
refine mode=y x1=0.80 y1=0.34 x2=1.0 y2=0.38

structure outf=mos1ex06_0.str
tonyplot  mos1ex06_0.str -set mos1ex06_0.set


go atlas

models  cvt srh print hcte.el hei nearflg

impact  selb  length.rel   lrel.el=0.02
contact name=gate n.poly
interface qf=3e10

material taun0=1e-7 taup0=1e-7
material  taurel.el=0.2e-12 taumob.el=0.2e-12


solve init

method newton trap temp.tol=1.e-4

solve vdrain=0.1 vstep=0.1 vfinal=3.3 name=drain

log outf=mos1ex06.log master

method newton trap temp.tol=1.e-4
solve vgate=0.025 vstep=0.025 vfinal=0.4  name=gate 
solve vgate=0.5 vstep=0.1 vfinal=5 name=gate 


tonyplot  mos1ex06.log -set mos1ex06_log.set

# Extract Substrate current peak value parameter....
extract init inf="mos1ex06.log"
extract name="nmax_isub_vd3.3" max(curve(abs(v."gate"),abs(i."substrate")))

# Extract gate voltage at the peak substrate current point....
extract init inf="mos1ex06.log"
extract name="nVgate_at_isubmax_vd3.3" x.val from curve(abs(v."gate"),abs(i."substrate")) where y.val=$"nmax_isub_vd3.3"

# Extract Gate current peak value parameter....
extract init inf="mos1ex06.log"
extract name="nmax_igate_vd3.3" (max(curve(abs(v."gate"),abs(i."gate"))))


# Extract gate voltage at the peak gate current point....
extract init inf="mos1ex06.log"
extract name="nVgate_at_isubmax_vd3.3" x.val from curve(abs(v."gate"),abs(i."gate")) where y.val=$"nmax_igate_vd3.3"


quit


