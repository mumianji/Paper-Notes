# (c) Silvaco Inc., 2018
go athena 

#
# the stretch command is used to redefine the drawn
# length of the poly using the cd value ....
#
set cd=0.8
#
line x loc=-0.5 spac=0.1 
line x loc=-0.4 spac=0.05
line x loc=-0.2 spac=0.01
line x loc=0.0  spac=0.1 
line x loc=0.2  spac=0.01 
line x loc=0.4  spac=0.05
line x loc=0.5  spac=0.1
#
line y loc=0.0 spac=0.001 
line y loc=0.2 spac=0.005
line y loc=0.5 spac=0.05
line y loc=0.8 spac=0.15 
line y loc=5 spac=1 
line y loc=10 spac=5 
#
init orientation=100 c.boron=1e14 space.mul=1.5
#
#pwell formation including masking off of the nwell
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#P-well Implant
# 
implant boron dose=8e12 energy=100 pears 

#
diffus temp=950 time=100 weto2 hcl=3
#
#N-well implant not shown -
#
# welldrive starts here
diffus time=50 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
diffus time=220 temp=1200 nitro press=1
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
diffus time=11 temp=925 dryo2 press=1.00 hcl=3
#
# Extract a design parameter ...
extract name="gateox" thickness oxide mat.occno=1 x.val=0.05

#
#vt adjust implant 
implant boron dose=9.5e11 energy=10 pearson 
#
#
depo poly thick=0.25 divi=10 
#
# from now on the situation is 2-D .... 
#
etch poly left p1.x=-0.25
#
etch poly right p1.x=0.25
#
stretch poly length=$"cd" div=6
#
method fermi compress
diffuse time=3 temp=900 weto2 press=1.0
#
implant phosphor dose=3.0e13 energy=20 pearson 
#
depo oxide thick=0.120 divisions=8
#
etch oxide dry thick=0.120
#
implant arsenic dose=5.0e15 energy=50 pearson 
#
method fermi compress
diffuse time=1 temp=900 nitro press=1.0
#
etch oxide right p1.x=($"cd"/2+0.1)
#
etch oxide left p1.x=-($"cd"/2+0.1)
#
deposit alumin thick=0.03 divi=2
# 
etch aluminum start x=-($"cd"/2+0.15) y=-10.00
etch cont x=($"cd"/2+0.15) y=-10.00
etch cont x=($"cd"/2+0.15) y=10.00
etch done x=-($"cd"/2+0.15) y=10.00
#
# Extract another design parameters...
# extract final S/D Xj...
extract name="nxj" xj silicon mat.occno=1 x.val=-($"cd"/2+0.2) junc.occno=1
# extract the long chan Vt...
extract name="n1dvt" 1dvt ntype vb=0.0 qss=1e10 x.val=0.0
# extract a curve of conductance versus bias....
extract start material="Polysilicon" mat.occno=1 \
	bias=0.0 bias.step=0.2 bias.stop=2 x.val=0.0
extract done name="sheet cond v bias" \
	curve(bias,1dn.conduct material="Silicon" mat.occno=1  region.occno=1)\
	outfile="extract.dat"
# extract the N++ regions sheet resistance...
extract name="n++ sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=-($"cd"/2+0.3) region.occno=1
# extract the sheet rho under the spacer, of the LDD region...
extract name="ldd sheet rho" sheet.res material="Silicon" \
	mat.occno=1 x.val=-($"cd"/2+0.05) region.occno=1
# extract the surface conc under the channel....
extract name="chan surf conc" surf.conc impurity="Net Doping" \
	material="Silicon" mat.occno=1 x.val=0.0


electrode name=gate x=0.0 y=0.1
electrode name=source x=-($"cd"/2+0.2)
electrode name=drain x=($"cd"/2+0.2)
electrode name=substrate backside

structure outfile=mos1ex15_0.str

# plot the structure
tonyplot  mos1ex15_0.str -set mos1ex15_0.set

############# Vt Test : Returns Vt, Beta and Theta ################
go atlas

# set material models
models cvt srh print  
contact name=gate n.poly
interface qf=3e10

# Bias the drain a bit...
solve vdrain=0.0 vstep=0.025 vfinal=0.1 name=drain

# Ramp the gate
log outf=mos1ex15_1.log master
solve vgate=0 vstep=0.25 vfinal=3.0 name=gate

# plot results
tonyplot  mos1ex15_1.log

# extract device parameters
extract init inf="mos1ex15_1.log"
extract name="nvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
	- abs(ave(v."drain"))/2.0)
extract name="nbeta" slope(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
	* (1.0/abs(ave(v."drain")))
extract name="ntheta" ((max(abs(v."drain")) * $"nbeta")/max(abs(i."drain"))) \
	- (1.0 / (max(abs(v."gate")) - ($"nvt")))

quit

