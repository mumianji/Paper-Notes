# (c) Silvaco Inc., 2018
############### Gamma Test for an NMOS transistor ################

go athena

#
line x loc=0 spac=0.1 
line x loc=0.2 spac=0.006
line x loc=0.4 spac=0.006
line x loc=0.5 spac=0.01 
#
line y loc=0.00 spac=0.002 
line y loc=0.2 spac=0.005
line y loc=0.5 spac=0.05
line y loc=0.8 spac=0.15 
#
init orientation=100 c.phos=1e14 space.mul=1

#pwell formation including masking off of the nwell
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#P-well Implant
# 
implant boron dose=8e12 energy=100 pears 

#
method grid.ox=0.02
diffus temp=950 time=100 weto2 hcl=3
#
#N-well implant not shown -
#
# welldrive starts here
diffus time=50 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
#
diffus time=220 temp=1200 nitro press=1
#
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
method grid.ox=0.0025
diffus time=11 temp=925 dryo2 press=1.00 hcl=3
#
#


#
#vt adjust implant 
implant boron dose=9.5e11 energy=10 pearson 

#
depo poly thick=0.2 divi=10 
#
#from now on the situation is 2-D
#
etch poly left p1.x=0.35
#
method fermi compress
diffuse time=3 temp=900 weto2 press=1.0
#
implant phosphor dose=3.0e13 energy=20 pearson 
#
depo oxide thick=0.120 divisions=8
#
etch oxide dry thick=0.120
#
implant arsenic dose=5.0e15 energy=50 pearson 
#
method fermi compress
diffuse time=1 temp=900 nitro press=1.0
#

#
etch oxide left p1.x=0.2
deposit alumin thick=0.03 divi=2
etch alumin right p1.x=0.18


# Extract a design parameter.....
extract name="gateox" thickness oxide mat.occno=1 x.val=0.49
# Extract another design parameters...
# extract final S/D Xj...
extract name="nxj" xj silicon mat.occno=1 x.val=0.1 junc.occno=1
# extract the long chan Vt...
extract name="n1dvt" 1dvt ntype vb=0.0 qss=1e10 x.val=0.49
# extract a curve of conductance versus bias....
extract start material="Polysilicon" mat.occno=1 bias=0.0 bias.step=0.2 bias.stop=2 x.val=0.45
extract done name="sheet cond v bias" curve(bias,1dn.conduct material="Silicon" mat.occno=1  region.occno=1) outfile="extract.dat"
# extract the N++ regions sheet resistance...
extract name="n++ sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.05 region.occno=1
# extract the sheet rho under the spacer, of the LDD region...
extract name="ldd sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.3 region.occno=1
# extract the surface conc under the channel....
extract name="chan surf conc" surf.conc impurity="Net Doping" material="Silicon" mat.occno=1 x.val=0.45

structure mirror right

electrode name=gate x=0.5 y=0.1
electrode name=source x=0.1
electrode name=drain x=0.9
electrode name=substrate backside

structure outfile=mos1ex05_0.str

# plot the structure
tonyplot  mos1ex05_0.str -set mos1ex05_0.set



############# Body Effect Test : Returns N-channel Gamma ################
go atlas


# set material models
models cvt srh print  

# Define the worksunction and the Qss....
contact name=gate n.poly
interface qf=3e10


# get initial solution
solve init
method  newton
solve prev


# Bias the drain a bit...
solve vdrain=0.025 vstep=0.025 vfinal=0.1 name=drain
# Ramp the gate
log outf=mos1ex05_1.log master
solve vgate=0 vstep=0.1 vdrain=0.1 vfinal=2 name=gate \
	compl=1.0e-7 cname=drain


# extract device parameters
extract init inf="mos1ex05_1.log"
extract name="nvt1" x.val from curve(abs(v."gate"),abs(i."drain")) where y.val=0.1e-6



# now open a dummy log file...
log off

# Now start again but with a substrate back-bias of a volt....
solve init
# Bias the substrate to -1 volt.....
solve vdrain=0.05 vstep=0.05 vfinal=0.1 name=drain
solve vdrain=0.1 vsubstrate=-0.25 vstep=-0.25 vfinal=-1 name=substrate

# Ramp the gate again with another opened logfile...
log outf=mos1ex05_2.log master
solve vgate=0 vstep=0.1 vdrain=0.1 vsubstrate=-1 \ 
	vfinal=2 name=gate compl=1.0e-7 cname=drain


extract init inf="mos1ex05_2.log"
extract name="nvt2" x.val from curve(abs(v."gate"),abs(i."drain")) where y.val=0.1e-6



# Calculate the parameter Gamma ....  with phi = 0.6 eV....
extract name="ngamma" ($"nvt2"-$"nvt1")/((sqrt(1+0.6))-sqrt(0.6))

tonyplot -overlay  mos1ex05_1.log mos1ex05_2.log -set mos1ex05_log.set

quit
