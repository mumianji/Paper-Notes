# (c) Silvaco Inc., 2018
go atlas
Title PIN diode small signal AC in 3D
#
# Small Signal AC Illumination
# PIN device description and initial solution
#
# SECTION 1: Mesh Specification
#
mesh space.mult=4.0 three.d
#
x.mesh loc=0.0  spacing=0.1
x.mesh loc=10.0 spacing=0.1
#
y.mesh loc=0.0   spacing=0.05
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.05
#
z.mesh loc=0.0   spacing=0.1
z.mesh loc=10.0   spacing=0.1
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode top
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6 
models srh auger conmob fldmob  
#
# SECTION 4: Initial Solution
#
solve    init
#
# SECTION 5: Optical source specification
#
#
beam     num=1 x.origin=5.0 y.origin=-1.0 z.origin=0.5 angle=90.0 wavelength=0.623 nx=101 nz=101 raytrace=optoex13
#
 
method  newton trap 
solve	 init
solve    vcathode=0.1
solve    vcathode=0.5
solve    vcathode=1.0
solve    vcathode=2.0
#
# SECTION 6: AC response
#
log	     outf=optoex13.log master
solve	 b1=.001 ss.phot ss.light=0.001 beam=1 freq=1e5 fstep=10 mult.f nfstep=6
#
tonyplot optoex13.log -set optoex13.set
quit
