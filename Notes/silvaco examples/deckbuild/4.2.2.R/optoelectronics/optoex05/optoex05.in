# (c) Silvaco Inc., 2018
go atlas
Title PIN photodiode spectral response
#
# PIN device spectral response.
#

# SECTION 1: Mesh Specification
#
mesh space.mult=4.0 
#
x.mesh loc=0.0  spacing=0.25
x.mesh loc=10.0 spacing=0.25
#
y.mesh loc=0.0   spacing=0.05
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.05
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6 
models srh auger conmob fldmob  
#
# SECTION 4: Initial Solution
#

	
solve    init outf=optoex05.str master

tonyplot optoex05.str -set optoex05_0.set



#
# SECTION 5: Optical source specification
#
beam     num=1 x.origin=5.0 y.origin=-1.0 angle=90.0 wavelength=.1
#
 
method newton trap 
solve    init
solve    vcathode=0.2
#
# SECTION 5: Spectral Response
#
log outf=optoex05.log master
solve prev b1=1 lambda=0.1
solve prev b1=1 lambda=0.2
solve prev b1=1 lambda=0.3
solve prev b1=1 lambda=0.4
solve prev b1=1 lambda=0.5
solve prev b1=1 lambda=0.6
solve prev b1=1 lambda=0.7
solve prev b1=1 lambda=0.8

tonyplot  optoex05.log -set optoex05_1.set
quit
