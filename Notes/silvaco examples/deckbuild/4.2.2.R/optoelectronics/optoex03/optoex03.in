# (c) Silvaco Inc., 2018
go atlas
Title Light Turn-Off Transient for PN diode 
#
# Time Domain Photogeneration Simulation
#
# SECTION 1: Mesh Specification
#
mesh space.mult=1.0 
#
x.mesh loc=0.0  spacing=2.5
x.mesh loc=10.0 spacing=2.5
#
y.mesh loc=0.0   spacing=0.05
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.05
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6 
models srh auger conmob fldmob  
#
# SECTION 4: Initial Solution
#

	
solve    init outf=optoex03_0.str master

tonyplot optoex03_0.str -set optoex03_0.set

#
# SECTION 5: Optical source specification
#
beam     num=1 x.origin=5.0 y.origin=-1.0 angle=90.0 wavelength=.623
#
 
method  newton trap 
solve    init
solve	 vcathode=0.1
solve	 vcathode=0.5 
solve	 vcathode=1.0 
solve	 vcathode=2.0 
#
# SECTION 6: Solve dc with light
# ramp the light off in the time domain
# light turns off in 1ns then takes several ns to settle out
#
solve	 b1=5
log	     outf=optoex03.log master
solve	 b1=0 ramp.lit ramptime=1e-9 tstop=10e-9 tstep=1e-12

tonyplot optoex03.log -set optoex03_1.set
 
quit
