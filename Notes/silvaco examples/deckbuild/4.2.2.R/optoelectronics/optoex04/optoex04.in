# (c) Silvaco Inc., 2018
go atlas
Title PIN diode small signal AC
#
# Small Signal AC Illumination
#
# PIN device description and initial solution
#
# SECTION 1: Mesh Specification
#
mesh space.mult=4.0 
#
x.mesh loc=0.0  spacing=0.25
x.mesh loc=10.0 spacing=0.25
#
y.mesh loc=0.0   spacing=0.05
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.05
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6 
models srh auger conmob fldmob  
#
# SECTION 4: Initial Solution
#

	
solve    init outf=optoex04_0.str master

tonyplot optoex04_0.str -set optoex04_0.set

#
# SECTION 5: Optical source specification
#
# define a multi-spectral beam normal to top (y=0.0) surface
# as follows:
# beam #1, origin at (2.5,-1.0), directed at an angle of 90 degrees,
# the beam spectrum is in the file optoex04.spec, the spectrum is
# sampled from 0.5 microns to 0.88 microns with 4 samples.
#
beam     num=1 x.origin=5.0 y.origin=-1.0 angle=90.0 power.file=optoex04.spec wavel.start=0.5 wavel.end=0.8 wavel.num=5
#
 
method  newton trap 
solve	 init
solve    vcathode=0.1
solve    vcathode=0.5
solve    vcathode=1.0
solve    vcathode=2.0
#
# SECTION 6: AC response
#
log	     outf=optoex04.log master
solve	 b1=.001 ss.phot ss.light=0.001 beam=1 freq=1e5 fstep=10 mult.f nfstep=6

tonyplot optoex04.log -set optoex04_1.set

quit
