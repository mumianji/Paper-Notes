# (c) Silvaco Inc., 2018
go atlas
title LED simulation
#
# Simulates a simple LED device
#
#-------------------------------------------------------------
# SECTION 1: MESH GENERATION
#-------------------------------------------------------------
# 4 um X 4 um
#
mesh space.mult=1.0
x.mesh l=0.0  spacing=0.25
x.mesh l=1.5  spacing=0.1
x.mesh l=2.5  spacing=0.1
x.mesh l=4.0  spacing=0.25
#
y.mesh l=0.0  spacing=0.1
y.mesh l=0.5  spacing=0.002
y.mesh l=0.75 spacing=0.05
y.mesh l=1.0  spacing=0.002
y.mesh l=1.1  spacing=0.002
y.mesh l=1.35 spacing=0.05
y.mesh l=1.6  spacing=0.002
y.mesh l=4.0  spacing=0.5
#
eliminate y.direction x.min=0.0 x.max=4.0 y.min=3.0 y.max=4.0
#
#-------------------------------------------------------------
# SECTION 2: REGIONS AND ELECTRODES
#-------------------------------------------------------------
# (graded heterojunctions outside of cladding layers)
#
region num=1 Material=GaAs   y.max=0.5
region num=2 Material=AlGaAs y.min=0.5 y.max=1.0 x.comp=0.35
# grad.12=0.1
region num=3 Material=GaAs   y.min=1.0 y.max=1.1 led
region num=4 Material=GaAs   y.min=1.6
region num=5 Material=AlGaAs y.min=1.1 y.max=1.6 x.comp=0.35
# grad.34=0.1
#
elec       num=1  name=anode x.min=1.5 x.max=2.5 y.min=0.0 y.max=0.0
elec       num=2  name=cathode bot
#
#-------------------------------------------------------------
# SECTION 3: DOPING PROFILES
#-------------------------------------------------------------
#
doping conc=5.0e18 p.type x.left=1.5 x.right=2.5 gaus char=0.5 ratio.lat=0.6
doping uniform region=1 p.type conc=1.e15
doping uniform region=2 p.type conc=1.e15
doping uniform region=3 p.type conc=1.e15
doping uniform region=4 n.type conc=2.e18
doping uniform region=5 n.type conc=2.e18
#
#
#---------------------------------------------------------------
# SECTION 4: MATERIAL MODELS
#---------------------------------------------------------------
#
# (radiative recombination constants same for AlGaAs and GaAs )
#
material copt=1.5e-10
material material=GaAs mun=6500 taurel.el=1.e-12 taumob.el=1.e-12 vsat=1.e7
material material=AlGaAs mun=2000 taurel.el=1.e-12 taumob.el=1.e-12 align=0.6

model auger optr srh bgn print
#
# If barrier exists at anode need to set workf=4.07+barrier height
#contact  name=anode workfun=4.07 
#
#----------------------------------------------------------------
# SECTION 5: OUTPUT FLAGS
#----------------------------------------------------------------
#
output con.band val.band recomb u.srh u.aug u.rad flowlines
#
#----------------------------------------------------------------
# SECTION 6: INITIAL SOLUTION
#----------------------------------------------------------------
#
# no carriers

solve init
#
#-----------------------------------------------------------------
# SECTION 7: BIAS RAMP 
#-----------------------------------------------------------------
#
probe name="Recombination" recombination integrate \
      left=0 right=4 top=1.0 bottom=1.1
probe name="Radiative" radiative integrate \
      left=0 right=4 top=1.0 bottom=1.1
# 0.0 to 2.0 V forward bias
#
log outf=optoex06.log master
method newton climit=1e-4 maxtrap=10
solve  l.wave=0.8 vanode=0.0 vstep=0.025 vfinal=2.0 name=anode 
save outfile=optoex06.str
#
# structure plot
tonyplot optoex06.str -set optoex06_0.set
# radiative recombination rate contours
tonyplot  optoex06.str -set optoex06_1.set
# luminous intensity vs bias
tonyplot optoex06.log -set optoex06_2.set
#
#------------------------------------------------------------------
# SECTION 8: EXTRACTION
#------------------------------------------------------------------
#
# Extract total and radiative components of recombination
# (can be used to calculate luminous efficiency)
#
measure u.total
measure u.radiative
#
quit
