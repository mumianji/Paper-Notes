# (c) Silvaco Inc., 2018
go atlas
Title PIN diode Gaussian beam with non-normal incidence and reflecting boundaries
#
# PIN DC solution
# PIN device description and initial solution
#
# SECTION 1: Mesh Specification
#
mesh space.mult=1.0 
#
x.mesh loc=0.0  spacing=0.25
x.mesh loc=10.0 spacing=0.25
#
y.mesh loc=0.0   spacing=0.05
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.05
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6 
models srh auger conmob fldmob  
#
# SECTION 4: Initial Solution
#
solve    init 
#
# SECTION 5: Optical source definition
#
# define a mono-spectral beam off-normal to top surface
# this beam is as follows:
# beam #1, originating at (1.0,-10.0), propogating at an angle
# of 70 degrees, with a single wavelength of 0.623 microns.  The
# beam has a gaussian profile with 100 rays and a sigma of 1 micron.
#
beam     num=1 x.origin=1.0 y.origin=-10.0 angle=70.0 wavelength=0.623 rays=100 sigma=1.0 gaussian
#
method   newton trap 
solve	 init
solve    vcathode=0.1
solve    vcathode=0.5
solve    vcathode=1.0
solve	 b1=0.1 outf=optoex14_1.str master onefile
#
go atlas
Title PIN diode Gaussian beam non-normal incidence
#
# PIN DC solution
# PIN device description and initial solution
#
# SECTION 1: Mesh Specification
#
mesh space.mult=1.0 
#
x.mesh loc=0.0  spacing=0.25
x.mesh loc=10.0 spacing=0.25
#
y.mesh loc=0.0   spacing=0.05
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.05
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6 
models srh auger conmob fldmob  
#
# SECTION 4: Initial Solution
#
solve    init 
#
# SECTION 5: Optical source definition
#
# define a mono-spectral beam off-normal to top surface
# this beam is as follows:
# beam #1, originating at (1.0,-10.0), propogating at an angle
# of 70 degrees, with a single wavelength of 0.623 microns.  The
# beam has a gaussian profile with 100 rays and a sigma of 1 micron.
#
beam     num=1 x.origin=1.0 y.origin=-10.0 angle=70.0 wavelength=0.623 rays=100 sigma=1.0 gaussian periodic
#
method   newton trap 
solve	 init
solve    vcathode=0.1
solve    vcathode=0.5
solve    vcathode=1.0
solve	 b1=0.1 outf=optoex14_2.str master onefile
#
# SECTION 6: Plot photogeneration rate
#
tonyplot optoex14_1.str  -add optoex14_2.str -set optoex14.set
quit
