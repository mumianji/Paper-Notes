go victoryprocess

# Define substrate properties, simulation domain and mesh resolution
#  and load the mask set 

Init material="silicon" depth=0.4 gasHeight=1.3 \
     layout="vpex02.lay" from="0.100,0.100" to="0.325,0.300" \
     resolution=0.01


# Etching with geometrical mode
#  A mask is used here for this step - mask layer with name "Active"

Etch dry thickness=0.150 mask="Active" reverse


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_1
Export structure=vpex02_1.str


# Deposition with geometrical mode

Deposit material="SiO2" conformal thickness=0.100


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_2
Export structure=vpex02_2.str


# Chemical mechanical polishing with geometrical mode 

Etch above=-0.060


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_3
Export structure=vpex02_3.str


# Etching with physical mode :
#  1. Setup properties (rates) for the various materials
#  2. Perform the physical etching step with the 
#      isotropic etching model

EtchDepoProperties name="myetch" \
	material="silicon" rate=0.06 \
	material="SiO2"    rate=0.09
Etch model="isotropic" materialProperties="myetch" time=1 maxCFL=0.8


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_4
Export structure=vpex02_4.str
 

# Oxidation with analytical mode

Oxidize thickness=0.005


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_5
Export structure=vpex02_5.str


# Deposition with geometrial mode
#  A mask is used here for this step - mask layer with name "Gate"

Deposit material="polysilicon" directional thickness=0.200 mask="Gate"


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_6
Export structure=vpex02_6.str

tonyplot3d vpex02_6.str -set vpex02_6.set


# Prepare the volume data mesh such that it is suitable for the following
#  doping steps.
# We usually recommend to put this volume data mesh set-up commands
#  at the beginning of the flow, but as demonstarted here those
#  commands can be used as long as no volume data have been generated.
#  The following IMPLANT statement will initialize the generation
#  of the volume data mesh.

# x-direction

Line X location=0.100 spacing=0.030
Line X location=0.280 spacing=0.005
Line X location=0.325 spacing=0.005

# y-direction

Line Y location=0.100 spacing=0.030
Line Y location=0.247 spacing=0.005
Line Y location=0.248 spacing=0.005
Line Y location=0.300 spacing=0.020

# z-direction

Line Z location=-0.185 spacing=0.010
Line Z location=-0.160 spacing=0.010
Line Z location=-0.100 spacing=0.050
Line Z location= 0.025 spacing=0.002
Line Z location= 0.080 spacing=0.040
Line Z location= 0.150 spacing=0.010
Line Z location= 0.400 spacing=0.050 

# Implantation with analytical mode

Implant arsenic energy=10 dose=1e14 tilt=7 rotation=22


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_7
Export structure=vpex02_7.str


# Annealing in inert ambient at 850 C for 20 min

Diffuse time=20 temp=850


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_8
Export structure=vpex02_8.str


# Oxidation with full physical mode

Diffuse wetO2 time=8 temperature=800 dif.coupled=false

Save   name=vpex02_9
Export structure=vpex02_9.str


# Deposition with physical mode
# The nonconformal model is used here. Its parameters
#  rate and ratio
# can be directly set within the DEPO statement, because 
# the selective deposition capability is not used here. 

Deposit material=nitride model="nonconformal" depositionrate=0.1 ratio=0.4 time=1.0


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_10
Export structure=vpex02_10.str

tonyplot3d vpex02_10.str -set vpex02_10.set


# Etching with geometrical mode

Etch dry thickness=0.113


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_11
Export structure=vpex02_11.str


# Implantation with Monte-Carlo mode

Implant arsenic energy=20 dose=1e15 tilt=7 rotation=22 montecarlo n.ion=2000000


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_12
Export structure=vpex02_12.str


# Assign the gate electrode

Electrode mask="Gate" material="poly"

Save   name=vpex02_13
Export structure=vpex02_13.str


# Deposition with geometrical mode - planarization

Deposit material=oxide max thickness=0.030


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_14
Export structure=vpex02_14.str

tonyplot3d vpex02_14.str -set vpex02_14.set


# Obtain aerial image of the mask layer with the name SD
#  The calculated aerial image is saved to the file aeimage.dat
#  A new mask layer with the name SD_LITHO is created

Lithography mask="SD" maskCriticalIntensity=0.14 \
            maskAperture=0.9 standardWl=DUVLINE resultImage="aeimage.dat" maskType="clear"

tonyplot aeimage.dat


# Etching with mask obtained from aerial image calculation

Etch material=oxide dry thickness=0.235 mask="SD_LITHO"


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_15
Export structure=vpex02_15.str


# Plug by filling the hole

Deposit material=tungsten conformal thickness=0.110


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_16
Export structure=vpex02_16.str


# Vias  

Etch above=-0.270 

Etch material=tungsten max thickness=0.110 right=0.240


# Save and Export simulation status in tonyplot3d format

Save   name=vpex02_17
Export structure=vpex02_17.str

tonyplot3d vpex02_17.str -set vpex02_17.set


# Export for device simulation with Victory Device

Export Victory(Conformal) structure=vpex02_18.str mirror.device="+x"

tonyplot3d vpex02_18.str -set vpex02_18.set

quit




