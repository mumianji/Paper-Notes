# (c) Silvaco Inc., 2018
# Noise Ex 04.
# Noise generated by a BJT.
#
go atlas

# MESH
#
mesh width=10
#
x.mesh loc=0 spac=0.1
x.mesh loc=2 spac=0.1
#
y.mesh loc=0 spac=0.001
y.mesh loc=1 spac=0.05

# REGION
#
region num=1 silicon

# ELECTRODE
#
electrode num=1 name=emitter   left   length=0.8
electrode num=2 name=base      right  length=0.5 y.max=0
electrode num=3 name=collector bottom

# DOPING
#
doping reg=1 uniform n.type conc=5e15 
doping reg=1 gauss   n.type conc=1e18 peak=1.0  char=0.2
doping reg=1 gauss   p.type conc=1e18 peak=0.05 junct=0.15 
doping reg=1 gauss   n.type conc=3e18 peak=0.0  junct=0.05 x.right=0.8
doping reg=1 gauss   p.type conc=5e19 peak=0.0  char=0.08  x.left=1.5

# MODEL
#
contact name=emitter n.poly
material taun0=5e-6 taup0=5e-6
models bipolar temperature=300 print

# SIMULATION
#
solve init
save outf=noiseex04_1.str

tonyplot noiseex04_1.str -set noiseex04_1.set

solve vcollector=2
solve vbase=0.1 
solve vbase=0.2 vstep=0.05 vfinal=0.8 name=base 

# AC/NOISE SOLUTION
#
output noise.all
log outf=noiseex04_tmp.log inport=base outport=collector z.param noise.v.all

solve noise.ss direct frequency=1.0E+06
save outf=noiseex04_2.str

tonyplot noiseex04_2.str -set noiseex04_2.set
tonyplot noiseex04_2.str -set noiseex04_3.set
tonyplot noiseex04_2.str -set noiseex04_4.set

# QUIT
#
quit
