# (c) Silvaco Inc., 2018
# Noise Ex 03.
# Noise generated by a mosfet.
#
go atlas

# MESH
#
mesh width=1
#
x.mesh loc=-1.25  spac=0.03125
x.mesh loc=-1.125 spac=0.03125
x.mesh loc=-1     spac=0.01
x.mesh loc=0      spac=0.2
x.mesh loc=1      spac=0.01
x.mesh loc=1.125  spac=0.03125
x.mesh loc=1.25   spac=0.03125
#
y.mesh loc=-0.018 spac=0.0045
y.mesh loc=-0.009 spac=0.0045
y.mesh loc=0      spac=0.002
y.mesh loc=0.1    spac=0.05
y.mesh loc=0.2    spac=0.01
y.mesh loc=1.5    spac=0.5

# REGION
#
region num=1 y.min=0 silicon
region num=2 y.max=0 oxide

# ELECTRODE
#
electrode name=source    x.max=-1.125         y.max=0
electrode name=gate      x.min=-1     x.max=1 y.max=-0.009
electrode name=drain     x.min=1.125          y.max=0
electrode name=substrate bottom

# DOPING
#
doping region=1           p.type conc=5e17   uniform 
doping x.max=-1 y.max=0.2 n.type conc=1.5e18 uniform
doping x.min=1  y.max=0.2 n.type conc=1.5e18 uniform

# MODEL
#
contact name=gate n.poly
models srh temperature=300 print

# SIMULATION
#
solve init
save outf=noiseex03_1.str

tonyplot noiseex03_1.str -set noiseex03_1.set

# DC SOLUTION
#
log outf=noiseex03_2.log
solve vgate=2.0 vdrain=0.0 vstep=0.25 vfinal=3.0 name=drain
log off

tonyplot noiseex03_2.log -set noiseex03_2.set

# AC/NOISE SOLUTION
#
log outf=noiseex03_3.log inport=gate outport=drain z.param noise.v
solve noise.ss direct frequency=1.0E+03 fstep=2.154435 nfsteps=27 mult.freq
log off

tonyplot noiseex03_3.log -add noiseex03_3.log -set noiseex03_3.set
tonyplot noiseex03_3.log -add noiseex03_3.log -set noiseex03_4.set

# QUIT
#
quit
