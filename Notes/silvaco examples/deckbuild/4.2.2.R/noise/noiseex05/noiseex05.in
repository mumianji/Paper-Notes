# (c) Silvaco Inc., 2018
# Noise Ex 05.
# Noise generated by a MESFET.
#
go atlas

# MESH
#
mesh width=50
#
x.mesh loc=0    spac=0.05
x.mesh loc=0.25 spac=0.05
x.mesh loc=0.28 spac=0.01
x.mesh loc=0.32 spac=0.01
x.mesh loc=0.35 spac=0.05
x.mesh loc=1.65 spac=0.05
x.mesh loc=1.68 spac=0.01
x.mesh loc=1.72 spac=0.01
x.mesh loc=1.75 spac=0.05
x.mesh loc=2    spac=0.05
#
y.mesh loc=0     spac=0.005
y.mesh loc=0.1   spac=0.01
y.mesh loc=0.5   spac=0.1

# REGION
#
region num=1 gaas

# ELECTRODE
#
electrode num=1 name=source           x.max=0.2 y.max=0.0
electrode num=2 name=gate   x.min=0.7 x.max=1.3 y.max=0.0
electrode num=3 name=drain  x.min=1.8           y.max=0.0

# DOPING
#
doping uniform reg=1     n.type conc=1.0e11
doping uniform y.max=0.1 n.type conc=2.0e17
doping gauss   x.max=0.3 n.type conc=2.0e20 peak=0.04 char=0.09 
doping gauss   x.min=1.7 n.type conc=2.0e20 peak=0.04 char=0.09 

# MODEL
#
contact name=gate work=4.87
material vsat=0.92e7 f.mnsndiff=noiseex05.lib f.mnspdiff=noiseex05.lib
trap donor e.level=0.45 density=1.0e16 degen=1 sign=1e-14 sigp=3e-14 fast
trap donor e.level=0.35 density=5.0e15 degen=1 sign=5e-13 sigp=2e-14 fast
models fldmob temperature=300 print

# SIMULATION
#
solve init
solve vgate=0.0 vdrain=0.0
save outf=noiseex05_1.str

tonyplot noiseex05_1.str -set noiseex05_1.set

# DC SOLUTION
#

solve vgate=-1.5 vdrain=0.00
solve vgate=-1.5 vdrain=0.01

log outf=noiseex05_2.log

solve vgate=-1.5 vdrain=0.02 vstep=0.01 vfinal=0.2 name=drain
solve vgate=-1.5 vdrain=0.25 vstep=0.05 vfinal=1.0 name=drain

log off

tonyplot noiseex05_2.log -set noiseex05_2.set

# AC/NOISE SOLUTION
#
log outf=noiseex05_3.log inport=gate outport=drain z.param noise.v.all
solve noise.ss direct frequency=1.0E+03 fstep=2.154435 nfsteps=27 mult.freq
log off

tonyplot noiseex05_3.log -add noiseex05_3.log -set noiseex05_3.set

# QUIT
#
quit
