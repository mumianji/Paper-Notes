# (c) Silvaco Inc., 2018
# Noise Ex 01.
# Noise generated by a resistor.
#
go atlas

# MESH
#
mesh width=2
#
x.mesh loc=0.0  spac=0.25
x.mesh loc=1.0  spac=0.25
#
# Noise works better with a fine mesh by the contacts.
# (Hence the y.mesh at loc=0.001 and loc=9.999)
#
y.mesh loc=0.0   spac=0.25
y.mesh loc=0.001 spac=0.25
y.mesh loc=9.999 spac=0.25
y.mesh loc=10.0  spac=0.25

# REGION
#
region num=1 silicon

# ELECTRODE
#
electrode num=1 top    name=ground
electrode num=2 bottom name=anode

# DOPING
#
# NB This doping gives a resistance of 10k.
#
doping uniform n.type conc=3.1211e16

# MODEL
#
models temperature=300 print

# SIMULATION
#
solve init
save outf=noiseex01_1.str

# DC SOLUTION
#
log outf=noiseex01_2.log
solve vanode=-10.0 vstep=0.5 vfinal=10.0 name=anode
log off

tonyplot noiseex01_2.log -set noiseex01_2.set

# AC/NOISE SOLUTION
#
# The Nyquist theorem gives the noise generated by a resistor as
#      <v^2> = 4kTR
#
# For a 10k resistor this gives
#      @300K <v^2> = 1.657e-16 V^2/Hz
#
solve vanode=0.0
log outf=noiseex01_3.log inport=anode noise.v
solve noise.ss direct frequency=1.0E+03 fstep=2.154435 nfsteps=27 mult.freq
log off

tonyplot noiseex01_3.log -set noiseex01_3.set

# QUIT
#
quit
