# (c) Silvaco Inc., 2018
# Noise Ex 02.
# Noise generated by a diode.
#
go atlas

# MESH
#
mesh width=1
#
x.mesh loc=0.0   spac=0.25
x.mesh loc=1.0   spac=0.25
#
y.mesh loc=0.0   spac=0.1
y.mesh loc=0.001 spac=0.1
y.mesh loc=4.5   spac=0.1
y.mesh loc=4.505 spac=0.005
y.mesh loc=5.495 spac=0.005
y.mesh loc=5.5   spac=0.1
y.mesh loc=9.999 spac=0.1
y.mesh loc=10.0  spac=0.1

# REGION
#
region num=1 silicon y.max=5.0
region num=2 silicon y.min=5.0

# ELECTRODE
#
electrode num=1 top    name=ground
electrode num=2 bottom name=anode

# DOPING
#
doping region=1 uniform n.type conc=1.0e16
doping region=2 uniform p.type conc=1.0e16

# MODEL
#
material material=silicon
models srh temperature=300 print

# SIMULATION
#
solve init
save outf=noiseex02_1.str

tonyplot noiseex02_1.str -set noiseex02_1.set

# DC SOLUTION
#
solve vanode=-3.0
log outf=noiseex02_2.log
solve vanode=-3.0 vstep=0.2 vfinal=-0.2 name=anode
solve vanode=-0.2 vstep=0.05 vfinal=0.8 name=anode
log off

tonyplot noiseex02_2.log -set noiseex02_2.set

# AC/NOISE SOLUTION
#
solve vanode=0.5
log outf=noiseex02_3.log inport=anode noise.v.all
solve noise.ss direct frequency=1.0E+03 fstep=2.154435 nfsteps=27 mult.freq
log off

tonyplot noiseex02_3.log -set noiseex02_3.set

# QUIT
#
quit
