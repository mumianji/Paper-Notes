# (c) Silvaco Inc., 2018
go atlas
Title    HEMT Id-Vd and Id-Vg characteristics
#
# SECTION 1: Mesh input

mesh

x.mesh loc=0.0  spac=0.05
x.mesh loc=0.75 spac=0.05

y.mesh loc=-0.02  spac=0.01
y.mesh loc=0.0    spac=0.01
y.mesh loc=0.03   spac=0.001
y.mesh loc=0.0425 spac=0.01
y.mesh loc=0.055  spac=0.001
y.mesh loc=0.2    spac=0.05

# SECTION 2: Structure Specification
#
region     num=1   material=GaAs y.min=0.03 y.max=0.055
region     num=2  material=AlGaAs y.max=0.03 x.composition=0.3 
region     num=3  material=AlGaAs y.min=0.055 x.composition=0.3
region     num=4  oxide y.min=-0.02 y.max=0 
# 
elec       num=1  name=source x.min=0.0 x.max=0.0 y.min=0.0 y.max=0.05
elec       num=2  name=gate   x.min=0.1 x.max=0.6 y.min=0.0 y.max=0.0
elec       num=3  name=drain  x.min=0.75 x.max=0.75 y.min=0.0 y.max=0.05
# 
doping     uniform y.min=0 y.max=0.03 n.type conc=1.e18
doping     uniform y.min=0.03 n.type conc=1.e15
doping     uniform x.min=0.0 x.max=0.05 y.min=0.03 y.max=0.05 \
   n.type conc=1.e18
doping     uniform x.min=0.70 x.max=0.75 y.min=0.03 y.max=0.05 \
   n.type conc=1.e18
#
interface x.min=0 x.max=0.75 y.min=-0.01 y.max=0.005 qf=-1.e12
#
# SECTION 3: Material Models
#
material taun0=1.e-9 taup0=1.e-9
material material=AlGaAs mun=2000 mup=350 affinity=3.82  nc300=5.7e17
#
model    fldmob srh  
model material=GaAs conmob 
#
contact  name=gate workfun=4.73 
#
# SECTION 4: Id-Vd calculation
#
method gummel  newton itlim=20 trap  maxtrap=6 vsatmod.inc=0.01 carriers=1 elect  
output con.band val.band 
solve vgate=0
#
save outf=hemtex03_0.str
tonyplot  hemtex03_0.str -set hemtex03_0.set
#
log outf=hemtex03_1.log  master
solve vdrain=0.05 
solve vdrain=0.10 
solve vdrain=0.125 
solve vdrain=0.15 
solve vdrain=0.20 
solve vdrain=0.30 
#
method  newton trap itlim=35  maxtrap=6    carriers=1 elect
solve vdrain=0.50  vstep=0.25  name=drain vfinal=3
#
save outf=hemtex03_1.str
tonyplot  hemtex03_1.log -set  hemtex03_1log.set
#
# SECTION 5: Id-Vg calculation
#
log off
solve init
method gummel  newton trap itlim=35  maxtrap=6 vsatmod.inc=0.01 carriers=1 elect
solve vgate=-0.9 vdrain=0
solve vdrain=0.1  vstep=0.1  name=drain vfinal=0.5
#
log outf=hemtex03_2.log master
method  newton trap itlim=35  maxtrap=6  carriers=1 elect
solve vgate=-0.8  vstep=0.1  name=gate vfinal=0.6
#
save outf=hemtex03_2.str
tonyplot  hemtex03_2.log -set  hemtex03_2log.set
#
quit
