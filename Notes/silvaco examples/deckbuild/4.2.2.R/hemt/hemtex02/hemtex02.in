# (c) Silvaco Inc., 2018
go atlas
Title    HEMT breakdown characteristics
#
# SECTION 1: Mesh input
#

mesh

x.mesh loc=0.0  spac=0.025
x.mesh loc=0.75 spac=0.025

y.mesh loc=-0.02  spac=0.01
y.mesh loc=0.0    spac=0.01
y.mesh loc=0.03   spac=0.001
y.mesh loc=0.0425 spac=0.01
y.mesh loc=0.055  spac=0.001
y.mesh loc=0.2    spac=0.05


# SECTION 2: Structure Specification
#
region     num=1  material=GaAs y.min=0.03 y.max=0.055
region     num=2  material=AlGaAs y.max=0.03 x.composition=0.3 
region     num=3  material=AlGaAs y.min=0.055 x.composition=0.3
region     num=4  oxide y.min=-0.02 y.max=0 
# 
elec       num=1  name=source x.min=0.0 x.max=0.0 y.min=0.0 y.max=0.05
elec       num=2  name=gate   x.min=0.1 x.max=0.6 y.min=0.0 y.max=0.0
elec       num=3  name=drain  x.min=0.75 x.max=0.75 y.min=0.0 y.max=0.05
# 
doping     uniform y.min=0 y.max=0.03 n.type conc=1.e18
doping     uniform y.min=0.03 n.type conc=1.e15
doping     uniform x.min=0.0 x.max=0.05 y.min=0.03 y.max=0.05 \
   n.type conc=1.e18
doping     uniform x.min=0.70 x.max=0.75 y.min=0.03 y.max=0.05 \
   n.type conc=1.e18
#
interface x.min=0 x.max=0.75 y.min=-0.01 y.max=0.005 qf=-1.e12
#
#
# SECTION 3: Material Models
#
material taun0=1.e-9 taup0=1.e-9
material material=AlGaAs mun=2000 mup=350 nc300=5.7e17
material align=0.6
#
model    fldmob srh 
model    material=GaAs conmob 
#
impact  selb an1=1.899e5 bn1=5.75e5 ap1=2.215e5 bp1=6.57e5 \
an2=1.899e5 bn2=5.75e5 ap2=2.215e5 bp2=6.57e5 egran=1.e20 betan=1.82 betap=1.75
#
contact  name=gate workfun=4.73 
#
# SECTION 4: Bias gate
#
method gummel  newton itlim=20 trap  maxtrap=6   
output con.band val.band 
#
solve vgate=0
save outf=hemtex02_0.str
tonyplot hemtex02_0.str -set hemtex02_0.set
#
# SECTION 5: Drain ramp
#
log outf=hemtex02.log master 
solve vdrain=0.025  vstep=0.025 vfinal=0.4 name=drain 
#
method newton trap itlim=35  maxtrap=6    
solve vdrain=0.50  vstep=0.25  name=drain vfinal=10 compl=1.e-3 e.comp=3
#
tonyplot  hemtex02.log -set hemtex02.set 
#
quit
