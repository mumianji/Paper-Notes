# (c) Silvaco Inc., 2018
#########################################################################
####  The present example is a CoolMOS example based on the reference: 
####  "Study of the Degradation of the Breakdown Voltage of a Super-Junction 
####  Power MOSFET due to Charge Imbalance", P. N. Kondekar et al., J. Korean 
####  Phys. Soc., Vol.48, (2006) pp.624.  
####  The off-state breakdown voltage is larger than 600V
##############################################################################

go athena
# 
line x loc=0.00 spac=0.25
line x loc=3.00 spac=0.10 
line x loc=10.00 spac=1.0
#
line y loc=39.00 spac=0.5
line y loc=40.00 spac=0.5
# 
init c.phosphor=1.0e18 orientation=100 space.mult=2
# 
epitaxy time=10 temp=1200 thickness=39 divisions=79 dy=0.10 ydy=0.00 c.phos=1.0e15

deposit oxide thickness=0.06 div=1
deposit poly thickness=0.35 div=3
deposit oxide thickness=0.35 div=3
deposit photores thickness=1 div=3

etch photores p1.x=3 left
etch oxide p1.x=3 left
etch poly p1.x=3 left
etch oxide p1.x=3 left

implant boron dose=1e14 energy=80

etch photores all

diffuse time=100 temp=1100

implant arsenic dose=3e15 energy=100
diffuse time=20 temp=1100

deposit oxide thickness=0.5 div=4
etch oxide thickness=0.5 

etch start x=0 y=-0.1 
etch cont x=0 y=0.8 
etch cont x=2.5 y=0.8 
etch done x=2.5 y=-0.1

diffuse time=1 temp=1100

deposit alum thickness=1 div=8
etch alum right p1.x=3.5

electrode name=source x=0
electrode name=gate x=5 y=-0.2
electrode name=drain backside

go atlas

# Super-junction: P-column
doping  p.type  conc=2.0e15 x.min=0.0 x.max=4.0 y.min=1.5 y.max=39.0  uniform

save outfile=powerex14_0.str
tonyplot  powerex14_0.str -set powerex14_0.set

models cvt  srh print 
impact selb

contact name=gate n.poly

#
method newton trap maxtraps=10 climit=1e-4  ir.tol=1e-30 ix.tol=1e-30

solve init
#

# Id-Vg @ Vg = 0V
log outf=powerex14.log
solve vdrain=0.03
solve vdrain=0.1
solve vdrain=0.25 vstep=0.25 vfinal=2 name=drain 
solve  vstep=1 vfinal=10 name=drain  
solve  vstep=5.0 vfinal=660 name=drain 
solve  vstep=0.25 vfinal=700 name=drain compl=1.e-7 cname=drain outf=powerex14_1.str master onefile 

extract name="bv" max(v."drain")

tonyplot powerex14_1.str -set powerex14_1.set
tonyplot powerex14.log -set powerex14_2.set

quit
