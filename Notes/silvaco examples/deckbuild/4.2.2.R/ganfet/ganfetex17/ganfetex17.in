# (c) Silvaco Inc., 2018
go victorydevice 

mesh space.mult=1

# x plane meshing
x.m l=0.0 s=0.25
x.m l=0.5 s=0.25
x.m l=1.0 s=0.25
x.m l=3.0 s=0.15
x.m l=3.5 s=0.15
x.m l=5.5 s=0.15
x.m l=6.0 s=0.15
x.m l=8   s=0.25
x.m l=8.5 s=0.25
x.m l=9   s=0.25

# y plane meshing
y.m l=0 s=0.25
y.m l=0.4 s=0.05
y.m l=0.5 s=0.001
y.m l=0.524 s=0.001
y.m l=0.525 s=0.001
y.m l=0.526 s=0.001
y.m l=0.6 s=0.05
y.m l=0.7 s=0.1
y.m l=1.0 s=0.2
y.m l=2.0 s=1

region num=1 x.min=0 x.max=9 y.min=0.5 y.max=0.525 mat=AlGaN x.comp=0.3 
region num=2 x.min=0 x.max=9 y.min=0.0 y.max=0.5 mat=nitride 
region num=3 x.min=0 x.max=9 y.min=0.525 y.max=2 mat=GaN  substrate 
region num=4 x.min=1 x.max=3 y.min=0 y.max=0.4 mat=oxide
region num=5 x.min=6 x.max=8 y.min=0 y.max=0.4 mat=oxide

elec num=1 name=source x.min=0 x.max=0.5 y.min=0.1 y.max=0.6
elec num=2 name=drain x.min=8.5 x.max=9 y.min=0.1 y.max=0.6
elec num=3 name=gate x.min=3.5 x.max=5.5 y.min=0.2 y.max=0.5 
elec num=4 substrate

contact name=gate work=5
contact name=source work=4
contact name=drain work=4

models print srh 
mobility albrct.n 
models POLARIZATION CALC.STRAIN TENSOR.SCALE=0 PSP.SCALE=0.8 PIEZO.SCALE=0.8
model lat.temp

thermcontact num=1 y.min=2 y.max=2 x.min=0 x.max=9 ext.temp=300 

output con.band val.band charge polar.charge band.par qss

method trap maxtrap=20 

solve init
save outf=ganfetex17_0.str

solve vdrain=1
log outf=ganfetex17_0.log
solve vgate=0 name=gate vfinal=-7.6 vstep=-0.2
log off

solve init
solve vgate=-3
log outf=ganfetex17_1.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off
#
solve init
solve vgate=-2
log outf=ganfetex17_2.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off
#
solve init
solve vgate=-1
log outf=ganfetex17_3.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off
#
solve init
solve vgate=0
log outf=ganfetex17_4.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off


go victorydevice 

mesh space.mult=1 three.d

x.m l=0.0 s=0.25
x.m l=0.5 s=0.25
x.m l=1.0 s=0.25
x.m l=3.0 s=0.15
x.m l=3.5 s=0.15
x.m l=5.5 s=0.15
x.m l=6.0 s=0.15
x.m l=8   s=0.25
x.m l=8.5 s=0.25
x.m l=9   s=0.25

y.m l=0 s=0.25
y.m l=0.4 s=0.05
y.m l=0.5 s=0.001
y.m l=0.524 s=0.001
y.m l=0.525 s=0.001
y.m l=0.526 s=0.001
y.m l=0.6 s=0.05
y.m l=0.7 s=0.1
y.m l=1.0 s=0.2
y.m l=2.0 s=1

z.m l=0 spac=0.5
z.m l=1 spac=0.5

region num=1 x.min=0 x.max=9 y.min=0.5 y.max=0.525 z.min=0 z.max=1 mat=AlGaN x.comp=0.3 
region num=2 x.min=0 x.max=9 y.min=0.0 y.max=0.5 z.min=0 z.max=1 mat=nitride 
region num=3 x.min=0 x.max=9 y.min=0.525 y.max=2 z.min=0 z.max=1 mat=GaN  substrate 
region num=4 x.min=1 x.max=3 y.min=0 y.max=0.4 z.min=0 z.max=1 mat=oxide
region num=5 x.min=6 x.max=8 y.min=0 y.max=0.4 z.min=0 z.max=1 mat=oxide

elec num=1 name=source x.min=0 x.max=0.5 y.min=0.1 y.max=0.6 z.min=0 z.max=1
elec num=2 name=drain x.min=8.5 x.max=9 y.min=0.1 y.max=0.6 z.min=0 z.max=1 
elec num=3 name=gate x.min=3.5 x.max=5.5 y.min=0.2 y.max=0.5 z.min=0 z.max=1
elec num=4 substrate

contact name=gate work=5
contact name=source work=4
contact name=drain work=4

models print srh 
mobility albrct.n 
models POLARIZATION CALC.STRAIN TENSOR.SCALE=0 PSP.SCALE=0.8 PIEZO.SCALE=0.8
model lat.temp 

thermcontact num=1 y.min=2 y.max=2 z.min=0 z.max=1 x.min=0 x.max=9 ext.temp=300 

output con.band val.band charge polar.charge band.par qss

method direct trap maxtrap=20 norm.scaling.local

solve init
save outf=ganfetex17_0_3D.str

solve name=drain vfinal=1 vstep=0.1

log outf=ganfetex17_0_3D.log
solve vgate=0 name=gate vfinal=-7.6 vstep=-0.2
log off

solve init
solve vgate=-3
log outf=ganfetex17_1_3D.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off
#
solve init
solve vgate=-2
log outf=ganfetex17_2_3D.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off
#
solve init
solve vgate=-1
log outf=ganfetex17_3_3D.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off
#
solve init
solve vgate=0
log outf=ganfetex17_4_3D.log
solve name=drain vdrain=0 vfinal=15 vstep=0.5
log off

tonyplot -overlay ganfetex17_0.log ganfetex17_0_3D.log -set ganfetex17_0.set
tonyplot -overlay ganfetex17_1.log ganfetex17_2.log ganfetex17_3.log ganfetex17_4.log ganfetex17_1_3D.log ganfetex17_2_3D.log ganfetex17_3_3D.log ganfetex17_4_3D.log -set ganfetex17_1.set
tonyplot3d ganfetex17_0_3D.str -set ganfetex17_2.set

quit