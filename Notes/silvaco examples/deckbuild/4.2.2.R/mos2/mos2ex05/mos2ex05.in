# (c) Silvaco Inc., 2018
go athena

line x loc=0 spac=0.1 
line x loc=0.2 spac=0.01
line x loc=0.5 spac=0.01 
#
line y loc=0.00 spac=0.01 
line y loc=0.2 spac=0.01
line y loc=0.5 spac=0.05
line y loc=0.8 spac=0.15 
#
init orientation=100 c.phos=1e14 space.mul=3

#pwell formation including masking off of the nwell
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#P-well Implant
# 
implant boron dose=8e12 energy=100 pears
#
diffus temp=950 time=100 weto2 hcl=3
#
#N-well implant not shown -
#
# welldrive starts here
diffus time=50 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
#
diffus time=220 temp=1200 nitro press=1
#
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
diffus time=11 temp=925 dryo2 press=1.00 hcl=3
#
#
extract thickness name="gateox" oxide mat.occno=1 min.v=140

#
#vt adjust implant 
implant boron dose=9.5e11 energy=10 pearson
#
depo poly thick=0.2 divi=10 
#
#from now on the situation is 2-D
#
etch poly left p1.x=0.35
#
method fermi compress
diffuse time=3 temp=900 weto2 press=1.0
#
implant phosphor dose=3.0e13 energy=20 pearson
#
depo oxide thick=0.120 divisions=8
#
etch oxide dry thick=0.120
#
implant arsenic dose=5.0e15 energy=50 pearson
#
method fermi compress
diffuse time=1 temp=900 nitro press=1.0
#

#
etch oxide left p1.x=0.2
deposit alumin thick=0.03 divi=2
etch alumin right p1.x=0.18
structure mirror right

electrode name=gate x=0.5 y=0.1
electrode name=source x=0.1
electrode name=drain x=0.9
electrode name=substrate backside

structure outfile=mos2ex05.str 

go devedit 


# Set Meshing Parameters
#
base.mesh height=0.05 width=0.05
#
bound.cond !apply max.slope=28 max.ratio=300 rnd.unit=0.001 line.straightening=1 align.points when=automatic
#
imp.refine imp="Net Doping" scale=log
imp.refine min.spacing=0.02
#
constr.mesh max.angle=90 max.ratio=300 max.height=10000 \
	max.width=10000 min.height=0.0001 min.width=0.0001

Mesh Mode=MeshBuild
refine mode=y x1=0.189 y1=0.211 x2=0.818 y2=0.23
refine mode=y x1=0.185 y1=0.215 x2=0.81 y2=0.242


structure outf=mos2ex05_0.str
tonyplot mos2ex05_0.str -set mos2ex05_0.set

go atlas

mesh inf=mos2ex05_0.str

material taurel.el=0.2e-12 taumob.el=0.2e-12  
 
models  bgn cvt hcte consrh auger print   
  
impact  selb length.rel lrel.el=0.02

contact name=gate n.poly

solve init 

method newton maxtrap=6 temp.tol=1.e-4 trap

output e.velocity

solve vgate=0.1
solve vgate=0.3
solve vgate=0.5 vstep=0.5 vfinal=3 name=gate 
solve vgate=3 

# Id-Vd caculations with EB model

log outf=mos2ex05_3_eb.log master

method block newton trap maxtrap=6  temp.tol=1.e-4 

solve vdrain=0.00625
solve vdrain=0.0125
solve vdrain=0.025
solve vdrain=0.1 vstep=0.1 vfinal=1 electr=3 

method  newton trap maxtrap=6  temp.tol=1.e-4 

solve vdrain=1.1 vstep=0.2 electr=3 vfinal=8.3 compliance=1.3e-3 cname=drain 
save outf=mos2ex05_3_eb.str


go atlas

mesh inf=mos2ex05_0.str


material  taurel.el=0.2e-12 taumob.el=0.2e-12 
 
models    bgn cvt hcte lat.temp consrh auger print   
  
impact  selb  length.rel  lrel.el=0.02

contact name=gate n.poly

thermcontact num=1 x.min=0 x.max=1 y.min=0.799 y.max=0.802 alpha=3000


solve init 

method  newton trap maxtrap=6  temp.tol=1.e-4 

output e.velocity

solve vgate=0.1
solve vgate=0.3
solve vgate=0.5 vstep=0.5 vfinal=3 name=gate 
solve vgate=3

# Id-Vd caculations with NEB model

log outf=mos2ex05_3_neb.log master

method block newton trap maxtrap=6  temp.tol=1.e-4
  
solve vdrain=0.00625
solve vdrain=0.0125
solve vdrain=0.025
solve vdrain=0.1 vstep=0.1 vfinal=1 electr=3 

method block newton trap maxtrap=6  temp.tol=1.e-4  
solve vdrain=1.1 vstep=0.2 electr=3 vfinal=9.1 
save outf=mos2ex05_3_neb.str

tonyplot  mos2ex05_3_eb.log -overlay mos2ex05_3_neb.log  -set mos2ex05log.set
    

quit


