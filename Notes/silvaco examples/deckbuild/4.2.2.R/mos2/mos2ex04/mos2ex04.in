# (c) Silvaco Inc., 2018
go athena

# Substrate mesh definition
line y loc=0    spac=0.05
line y loc=0.6  spac=0.2
line y loc=1       

line x loc=-0.6 spac=0.2
line x loc=0    spac=0.05
line x loc=0.2  spac=0.05
line x loc=0.6  spac=0.1

init orient=100 c.boron=1e15

# Pad oxide and nitride mask
deposit oxide thick=0.01 div=1
deposit nitride thick=0.25 div=1
deposit barrier thick=0.01 div=1
etch    barrier left p1.x=0
etch    nitride left p1.x=0
etch    oxide   left p1.x=0

implant boron dose=1e13 energy=100
etch barrier all

# Field oxidation 
method compress
diffuse tim=60 tem=1000 weto2 

# extract field oxide thickness
extract name="Field Tox" thickness material="SiO~2" mat.occno=1 x.val=-0.6

etch nitride all
etch oxide dry thickness=0.05

# gate oxidation
diff tim=12 temp=950 dryo2

# extract gate oxide thickness
extract name="Gate Tox" thickness material="SiO~2" mat.occno=1 x.val=0.5

# channel implant
implant boron dose=3e12 energy=25
depo poly thickness=0.2 div=5 c.phos=1e19

diff time=10 temp=950 nitrogen

electrode name=gate x=0
electrode name=substrate backside


structure outf=mos2ex04_0.str

tonyplot mos2ex04_0.str -set mos2ex04_0.set



go devedit simflags="-3d"

# load file with specified Z extent
init inf=mos2ex04_0.str z1=0 z2=1.1

region reg=1 mat=Silicon Z1=0 Z2=1.1 

region reg=2 mat="Silicon Oxide" Z1=0 Z2=1.1 

region reg=3 mat=PolySilicon  Z1=0.3 Z2=0.8 

region reg=4 name=source mat=Aluminum elec.id=1 work.func=0 color=0xffc8c8 pattern=0x7 Z1=0 Z2=0.1 \
	points="0.6,-0.007 0.6,0.005 0.6,0.05 0.2018696,0.05 0.15,0.05 0.15,0.004 0.15,-0.1 0.6,-0.1 0.6,-0.007"

region reg=5 name=drain mat=Aluminum elec.id=2 work.func=0 color=0xffc8c8 pattern=0x7 Z1=1 Z2=1.1 \
	points="0.15,0.05 0.15,0.004 0.15,-0.1 0.6,-0.1 0.6,-0.007 0.6,0.005 0.6,0.05 0.2018696,0.05 0.15,0.05"


impurity id=1 imp=Arsenic color=0x8c5d00 \
	x1=0.15 x2=0.6 y1=0 y2=0 \
	peak.value=1e+20 ref.value=1e+15 z1=0 z2=0.2 comb.func=Multiply \
	rolloff.y=both conc.func.y="Gaussian (Dist)" conc.param.y=0.25 \
	rolloff.x=both conc.func.x="Gaussian (Dist)" conc.param.x=0.15 \
	rolloff.z=both conc.func.z="Gaussian (Dist)" conc.param.z=0.15
impurity id=2 imp=Arsenic color=0x8c5d00 \
	x1=0.15 x2=0.6 y1=0 y2=0 \
	peak.value=1e+20 ref.value=1e+15 z1=0.9 z2=1.1 comb.func=Multiply \
	rolloff.y=both conc.func.y="Gaussian (Dist)" conc.param.y=0.25 \
	rolloff.x=both conc.func.x="Gaussian (Dist)" conc.param.x=0.15 \
	rolloff.z=both conc.func.z="Gaussian (Dist)" conc.param.z=0.15
impurity id=3 imp=Phosphorus color=0x8c5d00 \
	x1=0.15 x2=0.6 y1=0 y2=0 \
	peak.value=1e+18 ref.value=1e+15 z1=0.8 z2=1.1 comb.func=Multiply \
	rolloff.y=both conc.func.y="Gaussian (Dist)" conc.param.y=0.2 \
	rolloff.x=both conc.func.x="Gaussian (Dist)" conc.param.x=0.1 \
	rolloff.z=both conc.func.z="Gaussian (Dist)" conc.param.z=0.1
impurity id=4 imp=Phosphorus color=0x8c5d00 \
	x1=0.15 x2=0.6 y1=0 y2=0 \
	peak.value=1e+18 ref.value=1e+15 z1=0 z2=0.3 comb.func=Multiply \
	rolloff.y=both conc.func.y="Gaussian (Dist)" conc.param.y=0.2 \
	rolloff.x=both conc.func.x="Gaussian (Dist)" conc.param.x=0.1 \
	rolloff.z=both conc.func.z="Gaussian (Dist)" conc.param.z=0.1

constr.mesh region=1 default max.height=0.2 max.width=0.2

#
# Perform mesh operations
#
Mesh Mode=MeshBuild
refine mode=both x1=0.4098 y1=0.0048 x2=0.5988 y2=0.1799
refine mode=both x1=0.2216 y1=0.0376 x2=0.3718 y2=0.1849

z.plane z=0 spacing=0.1
#
z.plane z=0.1 spacing=0.1
#
z.plane z=0.3 spacing=0.05
#
z.plane z=0.55 spacing=0.2
#
z.plane z=0.8 spacing=0.05
#
z.plane z=1 spacing=0.1
#
z.plane z=1.1 spacing=0.1
#
z.plane max.spacing=1000000 max.ratio=1.5

structure outf=mos2ex04_1.str


go atlas


contact name=gate n.poly
interface qf=3e10

models cvt srh print

method carriers=1 electrons

solve vdrain=0.1

log outf=mos2ex04.log
solve vgate=0.1 vstep=0.1 vfinal=1.5 name=gate
save outf=mos2ex04_2.str

extract name="vt" xintercept(maxslope(curve(v."gate",i."drain"))) \
        - ave(v."drain")/2.0
extract name="subvt" \
        1.0/slope(maxslope(curve(v."gate",log10(i."drain"))))

tonyplot mos2ex04.log

quit
