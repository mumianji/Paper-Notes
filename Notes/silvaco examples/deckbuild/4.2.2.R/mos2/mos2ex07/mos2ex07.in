# (c) Silvaco Inc., 2018
go athena
#
line x loc=0 spac=0.1 
line x loc=0.7 spac=0.02
line x loc=1 spac=0.1 
#
line y loc=0.00 spac=0.02
line y loc=0.5 spac=0.1
line y loc=1.2 spac=0.15 
#
init orientation=100 c.phos=1.0e14 
#pwell formation including masking off of the nwell
#
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3
#
etch oxide thick=0.02
#
#P-well Implant
# 
implant boron dose=5.0e12 energy=100 pears amorphous
#
diffus temp=950 time=100 weto2 hcl=3
#
#N-well implant not shown -
#
# welldrive starts here
diffus time=50 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
#
diffus time=220 temp=1200 nitro press=1
#
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1
#
etch oxide all
#
#sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
#
etch oxide all
#
#gate oxide grown here:-
diffus time=10 temp=1000 dryo2 press=1.00 hcl=3
#
# Here is a possible Target for optimisation....
extract thickness oxide mat.occno=1 min.v=140 name="gateox"

#
#vt adjust implant 
implant boron dose=9.8e11 energy=25 pearson amorphous
#
# Now extract the surface and peak concs for channel profile optimisation...
extract name="peak channel conc" max.conc boron silicon mat.occno=1
extract name="surface channel conc" surf.conc boron silicon mat.occno=1
#
depo poly thick=0.387 divis=15
#
etch poly left p1.x=0.7
#
method fermi compress
diffuse time=10 temp=850 weto2 press=1.0
#
implant phosphor dose=3.0e13 energy=60 pearson amorphous
#
depo oxide thick=0.150 divisions=8
#
etch oxide dry thick=0.150
#
implant arsenic dose=5.0e15 energy=90 pearson amorphous
#
method fermi compress
diffuse time=5 temp=900 nitro press=1.0
#

#
etch oxide left p1.x=0.3
deposit alumin thick=0.03 divi=2
etch alumin right p1.x=0.25
structure mirror right



 
electrode name=gate x=1 y=0.0
electrode name=source x=0
electrode name=drain x=1.99
electrode name=substrate backside

structure outfile=mos2ex07.str 



tonyplot  mos2ex07.str -set mos2ex07_0.set


go atlas
# add workfunction and interface charge
contact name=gate n.polysilicon 
interf  qf=3E10


# select MOS models
models  cvt consrh  print   
impact  selb  

solve init
regrid potential abs ratio=0.1   cos.angl=0.6 

solve init

method newton trap ir.tol=1.e-25 ix.tol=1.e-25 clim.dd=1.e8
curvetrace end.val=1.e-3 contr.name=drain curr.cont \
           mincur=1e-10 nextst.ratio=1.3


# Open  log file
log     outf=mos2ex07.log

solve curvetrace
 


# plot the final curve....
tonyplot mos2ex07.log -set mos2ex07_log.set

quit
