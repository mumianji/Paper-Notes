# (c) Silvaco Inc., 2018
go athena
# SiGe MOS simulation

# Establish initial grid and substrate material
line x  location=0.0   spacing=0.075
line x  location=0.5   spacing=0.05
line x  location=0.75  spacing=0.075
line x  location=1.7   spacing=0.05
line x  location=2.2   spacing=0.05

line y  location=0.0   spacing=0.5
line y  location=1.0   spacing=0.5

init silicon c.phos=2e18 

epitaxy thick=2.5 divis=6 c.phos=4e16 time=25 temp=800
epitaxy thick=0.5 divis=12 c.phos=4e16 time=5  temp=800 dy=0.005 ydy=0


diff time=20 temp=900 dryo2

deposit nitride thick=.35 div=1

etch nitride left  p1.x=0.5

relax y.min=-2.5 dir.y=f 
relax y.min=-2.5 dir.y=f 


method grid.ox=0.05
diffuse time=60 temp=1000 weto2

strip nitride
etch oxide thick=0.05


# create the effect of selective epitaxy by 
# depositing and then removing from atop oxide  

# Deposit Silicon and Silicon Germanium layers
deposit silicon  thick=0.008 divis=4 c.boron=2e18
deposit silicon  thick=0.010 divis=4 c.phos=1e16
deposit sige     thick=0.015 divis=5 c.frac=0.35  c.phos=1e16
deposit silicon  thick=0.007 divis=4 c.phos=1e16

deposit photo thick=.4 divis=5
etch photo left x=0.8
# define the etch rates
rate.etch machine=test rie  n.m silicon isotropic=0.0 directional=20.0
rate.etch machine=test rie  n.m sige    isotropic=0.0 directional=20.0
rate.etch machine=test rie  n.m oxide   isotropic=0.0  directional=3.0

# perform the etch 
etch mach=test time=6. minutes dx.mult=0.25


strip


# deposit gate oxide to get exact thickness
deposit oxide    thick=0.016 div=1


# Deposit poly for the gate

deposit poly thick=0.2  divis=8 c.phos=1.e15

# Pattern the gate
etch poly       left  p1.x=1.7
etch oxide      thick=0.02

# implant source drain
implant boron dose=1e15 energy=20


rate.depo machine=BPSG nitride u.m  sigma.dep=0.20 smooth.win=0.5 \
        smooth.step=1 unidirec dep.rate=1.0 angle1=0.00

deposit machine=BPSG time=0.7 minute divis=8

deposit photo thick=.75 divis=7

etch photo    start x=0.9 y=10.
etch          cont  x=0.9 y=-10.
etch          cont  x=1.5 y=-10.
etch          done  x=1.5 y=10.


rate.etch machine=test1 nitride u.m  wet.etch isotropic=0.3
etch machine=test1 time=1. minutes dx.mult=0.2

rate.etch machine=test2 nitride u.m  rie      direct=0.3
etch machine=test2 time=1.5 minutes

strip

# Deposit and pattern the contact metal
# 
rate.depo machine=alum_depo alumin u.m  cvd dep.rate=1.0 step.cov=0.80
deposit machine=alum_depo time=.25 minute divis=5


etch aluminum right p1.x=1.9


# reflect the structure
structure right mirror


# Define the electrodes
electrode    name=source  left
electrode    name=drain   right
electrode    name=gate    x=2.5 y=-3.1
electrode    name=substrate backside

# Define impurity as acceptor or donor in SiGe material

impurity i.boron acceptor sige
impurity i.phos  donor    sige


structure outfile=mos2ex12_0.str


go atlas
 
# Material parameter, model, method, and output specification

material material=Si    taun0=1e-7  taup0=1e-7
material material=SiGe  taun0=1.e-8 taup0=1.e-8
model    bgn  consrh  auger fldmob conmob  print

contact name=gate n.polysilicon

# use newton method 
method newton 

# Perform zero bias solution
solve  

# Save the structure with band diagram and plot it
output con.band val.band
save outf=mos2ex12_1.str
tonyplot mos2ex12_1.str -set mos2ex12.set

solve vgate=1.0
solve vdrain=-0.1

log outf=mos2ex12.log
solve vstep=-0.2 vfinal=-1.0 name=gate

tonyplot mos2ex12.log 

quit
