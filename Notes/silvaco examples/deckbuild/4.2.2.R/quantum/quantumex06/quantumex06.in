# (c) Silvaco Inc., 2018
# Quasistatic C-V curves of an NMOS Capacitor from accumulation through
# inversion showing effects of quantisation such as reduced capacitance
# and threshold voltage shift. Around the flat band region the two curves
# match because quantisation is negligible in that case.

go atlas
#   BQP model with calibrated parameters
mesh

x.m l=0.0 s=1.0
x.m l=1.0 s=1.0
	
y.m l=-0.002 s=0.00004
y.m l=0.0    s=0.00004
y.m l=0.1    s=0.001
y.m l=1      s=0.05

region num=1 Silicon x.min=0.0 x.max=1.0 y.min=0 y.max=1.0
region num=2 SiO2    y.max=0.0

elec num=1 name=gate y.max=-0.002
elec num=2 name=substrate y.min=1.0

contact name=gate aluminum

doping conc=1e18 unif p.type

models srh bqp.n bqp.p fermi ni.fermi bqp.ngamma=1.35 bqp.nalpha=0.9 bqp.pgamma=1.0 bqp.palpha=0.7

method dvlimit=1.0 carriers=0

solve init

solve vgate=-3.0

output p.quantum

save outf=quantumex06_1.str

tonyplot quantumex06_1.str

log outfile=quantumex06_bqp.log
solve name=gate vgate=-3.0 vstep=0.03 vfinal=3.0 qscv 
log off 

save outf=quantumex06_2.str

go atlas
#Semi-classical model
mesh

x.m l=0.0 s=1.0
x.m l=1.0 s=1.0
	
y.m l=-0.002 s=0.00004
y.m l=0.0    s=0.00004
y.m l=0.1    s=0.001
y.m l=1      s=0.05

region num=1 Silicon x.min=0.0 x.max=1.0 y.min=0 y.max=1.0
region num=2 SiO2    y.max=0.0

elec num=1 name=gate y.max=-0.002
elec num=2 name=substrate y.min=1.0

contact name=gate aluminum

doping conc=1e18 unif p.type

models srh fermi ni.fermi

method dvlimit=1.0 carriers=0

solve init

solve vgate=-3.0

log outfile=quantumex06_scl.log
solve name=gate vgate=-3.0 vstep=0.03 vfinal=3.0 qscv 
logoff 

tonyplot -overlay quantumex06_bqp.log quantumex06_scl.log -set quantumex06.set




