# (c) Silvaco Inc., 2018
go atlas
Title Radiation interpreter function example
#
# Radiation interpreter function example
#
# SECTION 1: Mesh Specification
#
mesh three.d space.mult=1.0
#
x.mesh loc=0.0  spacing=1.0
x.mesh loc=10.0 spacing=1.0
#
y.mesh loc=0.0   spacing=0.1
y.mesh loc=5.0   spacing=0.2
y.mesh loc=10.0  spacing=0.1
#
z.m  l=0    spacing=1
z.m  l=5.0 s=1
z.m  l=10.0 spacing=1
#
# SECTION 2: Structure Specification
#
region num=1 material=Silicon
#
elec num=1 name=anode x.min=0.0 x.max=10.0 y.max=0.0
elec num=2 name=cathode bottom
#
doping uniform conc=1e14 n.type
doping gaus peak=0.0 char=0.1 conc=1e18 p.type dir=y
doping gaus peak=10.0 char=0.1 conc=1e18 n.type dir=y
#
# SECTION 3: Material Model Specification
#
material taup0=2.e-6 taun0=2.e-6
models srh auger conmob fldmob  
#
# define a function for the radiation
beam  f3.radiate=radex05.lib
#
# SECTION 4: Initial Solution
#

	 
solve	 init
#
# SECTION 5: Current vs. intensity
#
method newton
solve	 b1=0.000001 outf=radex05.str master
tonyplot3d radex05.str -set radex05.set

quit
