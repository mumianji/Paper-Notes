# (c) Silvaco Inc., 2018
go atlas
TITLE   a-si TFT simulation

mesh         
#
x.m		l=0	spac=0.5
x.m		l=2	spac=0.5
x.m		l=3.5	spac=1.0
x.m		l=5	spac=0.25
x.m		l=10	spac=1.5
x.m		l=15.	spac=0.25
x.m		l=16.5	spac=1.0
x.m		l=18	spac=0.5
x.m		l=20	spac=0.5
#
#
y.m		l=-0.1	spac=0.05
y.m		l=0	spac=0.0075
y.m		l=0.05	spac=0.01
y.m		l=0.1	spac=0.0075
y.m		l=0.5	spac=0.25
y.m		l=10.	spac=5
#
#
#  **************** regions ****************
#             1=oxide  2=silicon 3-oxide
#
region       num=1  y.max=0.    oxide
region       num=2  y.min=0.    y.max=0.1   silicon
region       num=3  y.min=0.1   oxide
#
#  **************** electrodes ****************
#    1=gate  2=substrate  3=source  4=drain
#
elec  num=1  x.min=5    x.max=15  y.min=-0.1 y.max=-0.1 name=gate
elec  num=2  substrate              name=substrate
elec  num=3  x.min=0.   x.max=2.    y.min=0.   y.max=0.   name=source
elec  num=4  x.min=18.0 x.max=20.0  y.min=0.   y.max=0.   name=drain
#
#  **************** doping profiles  ****************

doping     reg=2  uniform conc=1.e11 n.type
doping     reg=2  gauss conc=1.e20 n.type x.right=5 char=0.3
doping     reg=2  gauss conc=1.e20 n.type x.left=15 char=0.3

#
#
#     Set parameters for polysilicon
#
material region=2 mun=300 mup=30
#
defects nta=1.12e21 ntd=4.e20 wta=0.025 wtd=0.05 \
  nga=5.e17 ngd=1.5e18 ega=0.4 egd=0.4 wga=0.1 wgd=0.1 \
  sigtae=1.e-16 sigtah=1.e-14 sigtde=1.e-14 sigtdh=1.e-16  \ 
  siggae=1.e-16 siggah=1.e-14 siggde=1.e-14 siggdh=1.e-16  
#
contact	num=1 n.polysilicon
models  temp=300  
#
solve init
save outf=tftex03_0.str
tonyplot tftex03_0.str -set tftex03_0.set


#
# Electrode #1 - Gate, #2 - Substrate, #3 - Source, #4 - Drain
#

method newton
#
solve  vdrain=0.1
solve  vdrain=0.2
solve  vdrain=0.5
solve  vdrain=1 vstep=1. vfinal=5. name=drain
#
# Solve id versus vg curve
#

#
log outf=tftex03.log 
solve vstep=0.5 vfinal=20 name=gate vgate=0. vdrain=5

tonyplot  tftex03.log -set tftex03_log.set
#
quit

