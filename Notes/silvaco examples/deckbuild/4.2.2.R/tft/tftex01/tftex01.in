# (c) Silvaco Inc., 2018
go atlas
TITLE   a-si TFT simulation

mesh         
#
x.m		l=0	spac=0.5
x.m		l=2	spac=0.5
x.m		l=3.5	spac=1.0
x.m		l=5	spac=0.25
x.m		l=10	spac=1.5
x.m		l=15.	spac=0.25
x.m		l=16.5	spac=1.0
x.m		l=18	spac=0.5
x.m		l=20	spac=0.5
#
#
y.m		l=-0.1	spac=0.05
y.m		l=0	spac=0.0075
y.m		l=0.05	spac=0.01
y.m		l=0.1	spac=0.0075
y.m		l=0.5	spac=0.25
y.m		l=10.	spac=5
#
#
#  **************** regions ****************
#             1=oxide  2=silicon 3-oxide
#
region       num=1  y.max=0.    oxide
region       num=2  y.min=0.    y.max=0.1   silicon
region       num=3  y.min=0.1   oxide
#
#  **************** electrodes ****************
#    1=gate  2=substrate  3=source  4=drain
#
elec  num=1  x.min=5    x.max=15  y.min=-0.1 y.max=-0.1 name=gate
elec  num=2  substrate              name=substrate
elec  num=3  x.min=0.   x.max=2.    y.min=0.   y.max=0.   name=source
elec  num=4  x.min=18.0 x.max=20.0  y.min=0.   y.max=0.   name=drain
#
#  **************** doping profiles  ****************

doping     reg=2  uniform conc=7.e14 n.type
doping     reg=2  gauss conc=3.e18 n.type x.right=5 char=0.3
doping     reg=2  gauss conc=3.e18 n.type x.left=15 char=0.3


#   Set parameters for amorphus silicon
#
material region=2 mun=20 mup=1.5 nc300=2.5e20 \
 nv300=2.5e20 eg300=1.9
#
defects nta=1.e21 ntd=1.e21 wta=0.033 wtd=0.049 \
  nga=1.5e15 ngd=1.5e15 ega=0.62 egd=0.78 wga=0.15 wgd=0.15 \
  sigtae=1.e-17 sigtah=1.e-15 sigtde=1.e-15 sigtdh=1.e-17  \ 
  siggae=2.e-16 siggah=2.e-15 siggde=2.e-15 siggdh=2.e-16  
#
contact	num=1 alum
models  temp=300  
#

method itlimit=30
solve init

save outf=tftex01_0.str

tonyplot  tftex01_0.str -set tftex01_0.set
 
#
# Electrode #1 - Gate, #2 - Substrate, #3 - Source, #4 - Drain
#

method  gummel newton 
solve  vgate=-1.0

method  newton 
solve  vdrain=0.1
solve  vdrain=0.2
solve  vdrain=0.5
solve  vdrain=1 vfinal=5 vstep=1 name=drain
#
# Solve id versus vg curve
#

#
log outf=tftex01_1.log 
solve vstep=0.5 vfinal=20. name=gate vdrain=5 
#
tonyplot  tftex01_1.log  -set tftex01_log.set

quit
