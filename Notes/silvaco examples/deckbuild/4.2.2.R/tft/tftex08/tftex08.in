# (c) Silvaco Inc., 2018
go atlas
TITLE polysilicon TFT simulation

mesh   nx=30  ny=30  
#
x.m          n=1      l=0         r=1
x.m          n=30     l=20.       r=1
#
#
y.m          n=1      l=-0.1    r=1
y.m          n=5      l=0       r=1
y.m          n=19     l=0.1     r=1
y.m          n=30     l=10.     r=1.6
#
#
#  **************** regions *******************************
#             1=oxide  2=silicon 3-oxide
#
region       num=1  y.max=0.    oxide
region       num=2  y.min=0. y.max=0.1   silicon
region       num=3  y.min=0.1   oxide
#
#  **************** electrodes ****************************
#    1=gate  2=substrate  3=source  4=drain
#
elec  num=1  x.min=5   x.max=15  y.min=-0.1 y.max=-0.1 name=gate
elec  num=2  substrate            name=substrate
elec  num=3  x.min=0.  x.max=2.  y.min=0.   y.max=0.   name=source
elec  num=4  x.min=18. x.max=20. y.min=0.   y.max=0.   name=drain
#
#  **************** doping profiles ***********************
#
doping     reg=2  uniform conc=1.e11 n.type
doping     reg=2  gauss conc=1.e20 n.type x.right=5 char=0.3
doping     reg=2  gauss conc=1.e20 n.type x.left=15 char=0.3

#
#
#     Set parameters for polysilicon
#
material region=2 mun=300 mup=30
#
# Use the discrete defect model 
defects  f.tftacc=tftex08.lib f.tftdon=tftex08.lib  \ 
  nta=0 ntd=0 wta=0.025 wtd=0.05 \
  nga=0 ngd=0 ega=0.4 egd=0.4 wga=0.1 wgd=0.1 \
  sigtae=1.e-16 sigtah=1.e-14 sigtde=1.e-14 sigtdh=1.e-16  \ 
  siggae=1.e-16 siggah=1.e-14 siggde=1.e-14 siggdh=1.e-16  \
  afile=acceptor.dat dfile=donor.dat
#
contact	num=1 n.polysilicon
models  temp=300  
#
method newton

solve init
save outf=tftex08_0.str
tonyplot tftex08_0.str -set tftex08_0.set


#
# Electrode #1 - Gate, #2 - Substrate, #3 - Source, #4 - Drain
#

method newton
#
solve  vdrain=0.1
solve  vdrain=0.2
solve  vdrain=0.5
solve  vdrain=1 vstep=1. vfinal=5. name=drain
#
# Solve id versus vg curve
#

#
log outf=tftex08_1.log 
solve vstep=0.5 vfinal=20 name=gate vgate=0. vdrain=5

tonyplot tftex08_1.log -set tftex08_1.set
#
quit


